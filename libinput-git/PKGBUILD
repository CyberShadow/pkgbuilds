pkgname=libinput-git
pkgver=1.7.0.r155.g980ad92
pkgrel=1

pkgdesc='library to handle input devices'
url='http://freedesktop.org/wiki/Software/libinput/'
arch=('i686' 'x86_64')
license=('MIT')

depends=('mtdev' 'libevdev' 'libwacom')
makedepends=('git' 'doxygen')
checkdepends=('check')

provides=("libinput=$pkgver")
conflicts=('libinput')

source=('git+https://anongit.freedesktop.org/git/wayland/libinput.git')

sha1sums=('SKIP')

pkgver() {
    cd libinput
    git describe | sed 's/-/.r/; s/-/./'
}

build() {
    cd libinput
    autoreconf -fi
    ./configure --prefix=/usr --enable-libwacom --disable-static
    make
}

check() {
    cd libinput
    # So it turns out that libinput actually is attempting to create files in
    # /run/udev/rules.d which require root.  I am also told that libinput
    # further requires root for its use of uinput (this is not documented
    # anywhere).  Because of this libcheck seems to fail with an unrelated
    # message or surpresses diagnostic messages from litest.c.
    # https://github.com/libcheck/check/issues/18

    # As a result of all of this nonsense, we won't be running any tests.
    #make check
}

package() {
    cd libinput
    make DESTDIR="$pkgdir" install
    install -Dm0644 COPYING "$pkgdir"/usr/share/licenses/"$pkgname"/COPYING
}
