pkgname=libinput-git
pkgver=1.2.2.r21.g5152d94
pkgrel=1

pkgdesc='library to handle input devices'
url='http://freedesktop.org/wiki/Software/libinput/'
arch=('i686' 'x86_64')
license=('MIT')

depends=('mtdev' 'libevdev' 'libwacom')
makedepends=('git')

# XXX lcarlier, what a guy.
provides=("libinput=$pkgver")
conflicts=('libinput')

source=('git+https://anongit.freedesktop.org/git/wayland/libinput.git')

sha1sums=('SKIP')

pkgver() {
    cd libinput
    git describe | sed 's/-/.r/; s/-/./'
}

build() {
    cd libinput
    ./autogen.sh --prefix=/usr --enable-libwacom --disable-static
    make
}

package() {
    cd libinput
    make DESTDIR="$pkgdir" install
    install -Dm644 COPYING "$pkgdir"/usr/share/licenses/"$pkgname"/COPYING
}
