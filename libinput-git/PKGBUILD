pkgname=libinput-git
pkgver=1.7.0.r15.g078d5cb
pkgrel=1

pkgdesc='library to handle input devices'
url='http://freedesktop.org/wiki/Software/libinput/'
arch=('i686' 'x86_64')
license=('MIT')

depends=('mtdev' 'libevdev' 'libwacom')
makedepends=('git')

provides=("libinput=$pkgver") # XXX Sigh
conflicts=('libinput')

source=('git+https://anongit.freedesktop.org/git/wayland/libinput.git')

sha1sums=('SKIP')

pkgver() {
    cd libinput
    git describe | sed 's/-/.r/; s/-/./'
}

build() {
    cd libinput
    autoreconf -fi
    ./configure --prefix=/usr --enable-libwacom --disable-static
    make
}

check() {
    cd libinput
    make check
}

package() {
    cd libinput
    make DESTDIR="$pkgdir" install
    install -Dm0644 COPYING "$pkgdir"/usr/share/licenses/"$pkgname"/COPYING
}
