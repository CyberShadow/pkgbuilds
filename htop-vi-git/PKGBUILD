pkgname=htop-vi-git
pkgver=2.0.2.r49.gb1028e0
pkgrel=2

pkgdesc='interactive process viewer patched with rudimentary vi style motions'
url='http://htop.sourceforge.net/'
arch=('i686' 'x86_64')
license=('GPL')

depends=('glibc' 'ncurses')
makedepends=('git')

provides=('htop')
conflicts=('htop')

source=('git+https://github.com/hishamhm/htop'
        'basic-vi-keys.patch')

sha1sums=('SKIP'
          '686508a2028e3232d44e36c5a90909cb54d901eb')

pkgver() {
    cd htop
    git describe --tags | sed 's/-/.r/; s/-/./'
}

prepare() {
    cd htop
    sed -i '1 s/python$/&2/' scripts/MakeHeader.py
    patch -Np1 -i "$srcdir"/basic-vi-keys.patch
}

build() {
    cd htop
    autoreconf -fi
    ./configure --prefix=/usr --enable-cgroup --enable-unicode
    make
}

package() {
    cd htop
    make DESTDIR="$pkgdir" install 
}
