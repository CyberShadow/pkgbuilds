pkgname=htop-vi-git
pkgver=2.0.1.r31.g7d72715
pkgrel=1

pkgdesc='interactive process viewer patched with rudimentary vi style motions'
url='http://htop.sourceforge.net/'
arch=('i686' 'x86_64')
license=('GPL')

depends=('glibc' 'ncurses')
makedepends=('git')

provides=('htop')
conflicts=('htop')

source=('git+https://github.com/hishamhm/htop'
        'htop-vi.patch')

sha1sums=('SKIP'
          '1e01cc530f286fc950b4cee4c35ad99b574dd5d0')

pkgver() {
    cd htop
    git describe --tags | sed 's/-/.r/; s/-/./'
}

prepare() {
    cd htop
    patch -Np1 -i "$srcdir"/htop-vi.patch
}

build() {
    cd htop
    ./autogen.sh
    ./configure --prefix=/usr --enable-cgroup --enable-unicode
    make
}

package() {
    cd htop
    make DESTDIR="$pkgdir" install 
}
