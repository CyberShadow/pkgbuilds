pkgname=ponyc-git
pkgver=0.2.0.r27.gf423cb1
pkgrel=1

pkgdesc='An actor model, capabilities, high performance programming language.'
url='http://ponylang.org/'
arch=('x86_64')
license=('BSD')

options=('staticlibs' 'debug' '!strip')

depends=('zlib' 'ncurses' 'gcc-libs')
makedepends=('git' 'llvm')
checkdepends=('pcre2')
optdepends=('pcre2: Needed for the regex package.')

provides=('ponyc')
conflicts=('ponyc')

source=('git://github.com/CausalityLtd/ponyc#branch=llvm37')
install='ponyc-git.install'

md5sums=('SKIP')

pkgver() {
    cd ponyc
    git describe --tags | sed 's/-/.r/; s/-/./'
}

build() {
    cd ponyc
    make config=release prefix=/usr
}

check() {
    cd ponyc
    make config=release prefix=/usr test
}

package() {
    cd ponyc
    # The current makefile uses odd logic to try and symlink the ponyc to the
    # prefix but ignoring the $(destdir).  In fact, the prefix= command-line is
    # ignored completely for this innvocation.

    # Given the issues with the makefile I will instead install pony manually
    # while providing a few niceties that allow multiple versions of the
    # standard packages to be installed at the same time.
    # make config=release prefix=/usr destdir="$pkgdir" symlink=no install

    _tag=$(git describe --always --tags)
    install -Dm755 build/release/ponyc "$pkgdir"/usr/lib/pony/"$_tag"/ponyc
    install -Dm644 build/release/libponyrt.a "$pkgdir"/usr/lib/libponyrt.a
    install -Dm644 build/release/libponyc.a "$pkgdir"/usr/lib/libponyc.a
    install -Dm644 src/libponyrt/pony.h "$pkgdir"/usr/include/pony.h

    # Ponyc is designed to read its standard packages from the same directory.
    install -dm755 "$pkgdir"/usr/bin
    ln -sf /usr/lib/pony/"$_tag"/ponyc "$pkgdir"/usr/bin/ponyc
    cp -dr --no-preserve=ownership packages/* "$pkgdir"/usr/lib/pony/"$_tag"

    # Let's include the examples as documentation is scarce.
    install -dm755 "$pkgdir"/usr/share/doc/pony
    cp -dr --no-preserve=ownership examples "$pkgdir"/usr/share/doc/pony

    install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE
}
