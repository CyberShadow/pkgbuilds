pkgname=ponyc-git
pkgver=0.4.0.r506.ge84506dd6
pkgrel=1

pkgdesc='actor model and capabilities programming language'
url='http://ponylang.org/'
arch=('x86_64')
license=('BSD')

options=('staticlibs' 'debug' '!strip')

depends=('zlib' 'ncurses')
makedepends=('git' 'llvm')
checkdepends=('pcre2')
optdepends=('pcre2: Needed for the regex package.')

provides=('ponyc')
conflicts=('ponyc')

source=('git+https://github.com/ponylang/ponyc')

sha1sums=('SKIP')

pkgver() {
    cd ponyc
    git describe --tags | sed 's/-/.r/; s/-/./'
}

build() {
    cd ponyc
    make config=release prefix=/usr

    mkdir docs
    build/release/ponyc packages/stdlib -r expr -g -o docs
}

check() {
    cd ponyc
    # XXX net/Broadcast can fail https://github.com/ponylang/ponyc/issues/602
    make config=release prefix=/usr test || true
}

package() {
    cd ponyc
    # Given the issues with the makefile I will install pony manually.
    # make config=release prefix=/usr destdir="$pkgdir" symlink=no install
    install -Dm0755 build/release/ponyc "$pkgdir"/usr/lib/pony/ponyc
    install -Dm0644 src/libponyrt/pony.h "$pkgdir"/usr/include/pony.h

    install -Dm0644 build/release/libponyc.a "$pkgdir"/usr/lib/libponyc.a
    install -Dm0644 build/release/libponyrt.a "$pkgdir"/usr/lib/libponyrt.a

    # Ponyc is designed to read its standard packages from the same directory.
    # With this we install the binary to package directory and add a symlink
    # to /bin for PATH access.
    install -dm0755 "$pkgdir"/usr/bin
    ln -sf /usr/lib/pony/ponyc "$pkgdir"/usr/bin/ponyc
    cp -dr --no-preserve=ownership packages/* "$pkgdir"/usr/lib/pony

    # Documentation.
    install -dm0755 "$pkgdir"/usr/share/doc/pony
    cp -dr --no-preserve=ownership docs "$pkgdir"/usr/share/doc/pony
    cp -dr --no-preserve=ownership examples "$pkgdir"/usr/share/doc/pony

    install -Dm0644 LICENSE "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE
}
