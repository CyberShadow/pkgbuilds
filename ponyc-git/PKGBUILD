pkgname=ponyc-git
pkgver=0.2.1.r96.g57461c4
pkgrel=1

pkgdesc='An actor model, capabilities, high performance programming language.'
url='http://ponylang.org/'
arch=('x86_64')
license=('BSD')

options=('staticlibs' 'debug' '!strip')

depends=('zlib' 'ncurses')
makedepends=('git' 'llvm')
checkdepends=('pcre2')
optdepends=('pcre2: Needed for the regex package.')

provides=('ponyc')
conflicts=('ponyc')

source=('git://github.com/CausalityLtd/ponyc#branch=llvm37')
install='ponyc-git.install'

sha1sums=('SKIP')

pkgver() {
    cd ponyc
    git describe --tags | sed 's/-/.r/; s/-/./'
}

build() {
    cd ponyc
    make config=release prefix=/usr

    mkdir docs
    build/release/ponyc packages/stdlib -rexpr -g -o docs
}

check() {
    cd ponyc
    make config=release prefix=/usr test
}

package() {
    cd ponyc
    # XXX Cannot reasonably use the provided Makefile for installation
    # The current makefile uses some logic to try and symlink the ponyc to the
    # prefix but ignoring the $(destdir).  In fact, the prefix= command-line is
    # ignored completely for this innvocation.

    # Given the issues with the makefile I will instead install pony manually.
    # make config=release prefix=/usr destdir="$pkgdir" symlink=no install

    install -Dm755 build/release/ponyc "$pkgdir"/usr/lib/pony/ponyc
    install -Dm644 src/libponyrt/pony.h "$pkgdir"/usr/include/pony.h

    install -Dm644 build/release/libponyc.a "$pkgdir"/usr/lib/libponyc.a
    install -Dm644 build/release/libponyrt.a "$pkgdir"/usr/lib/libponyrt.a

    # Ponyc is designed to read its standard packages from the same directory.
    # With this we install the binary to package directory and add a symlink
    # to /bin for PATH access.
    install -dm755 "$pkgdir"/usr/bin
    ln -sf /usr/lib/pony/ponyc "$pkgdir"/usr/bin/ponyc
    cp -dr --no-preserve=ownership packages/* "$pkgdir"/usr/lib/pony

    # Documentation.
    install -dm755 "$pkgdir"/usr/share/doc/pony
    cp -dr --no-preserve=ownership docs "$pkgdir"/usr/share/doc/pony
    cp -dr --no-preserve=ownership examples "$pkgdir"/usr/share/doc/pony

    install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE
}
