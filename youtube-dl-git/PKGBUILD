pkgname=youtube-dl-git
pkgver=r9359.237c03c
pkgrel=1

pkgdesc='A small command-line program to download videos from many different sites.'
url='http://rg3.github.com/youtube-dl'
arch=('any')
license=('custom')

depends=('python')
makedepends=('git')

optdepends=('ffmpeg: for video post-processing')

provides=('youtube-dl')
conflicts=('youtube-dl')

source=('git://github.com/rg3/youtube-dl.git')

md5sums=('SKIP')

pkgver() {
	cd youtube-dl
    printf 'r%s.%s' "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
	cd youtube-dl
	sed -i 's:etc/bash_completion.d:share/bash-completion/completions:' setup.py
}

package() {
	cd youtube-dl
	python devscripts/bash-completion.py
	python setup.py install --root="$pkgdir"/ --optimize=1
	mv "$pkgdir"/usr/share/bash-completion/completions/youtube-dl.bash-completion "$pkgdir"/usr/share/bash-completion/completions/youtube-dl
	rm -rf "$pkgdir"/usr/etc
	install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE
}
