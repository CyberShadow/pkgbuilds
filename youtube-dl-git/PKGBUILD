pkgname=youtube-dl-git
pkgver=2016.02.13.r50.g8864124
pkgrel=1

pkgdesc='A command-line program to download videos from different sites'
url='http://rg3.github.com/youtube-dl'
arch=('any')
license=('custom')

depends=('python')
makedepends=('git' 'python-sphinx')

optdepends=('ffmpeg: For video post-processing')

provides=('youtube-dl')
conflicts=('youtube-dl')

source=('git+https://github.com/rg3/youtube-dl.git')

sha1sums=('SKIP')

pkgver() {
    cd youtube-dl
    git describe | sed 's/-/.r/; s/-/./'
}

package() {
    cd youtube-dl
    python setup.py install --root="$pkgdir" --optimize=1

    # Generate the rest of the files manually; this is because youtube-dl seems
    # unable to use correct directory locations consistently.  Let's do it
    # manually and more neatly.  Skipping fish because that shell is terrible.
    python devscripts/zsh-completion.py
    python devscripts/bash-completion.py
    install -Dm644 youtube-dl.zsh "$pkgdir"/usr/share/zsh/site-functions/_youtube-dl
    install -Dm644 youtube-dl.bash-completion "$pkgdir"/usr/share/bash-completion/completions/youtube-dl

    make -C docs man
    install -Dm644 docs/_build/man/youtube-dl.1 "$pkgdir"/usr/share/man/man1/youtube-dl.1

    install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE

    # Clean up.
    rm -rf "$pkgdir"/usr/etc
    rm -rf "$pkgdir"/usr/share/doc
}
