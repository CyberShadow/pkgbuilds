# pkgver=$(curl -s "$url" | awk '/<title>/ { print $2 }')
pkgname=par
pkgver=1.52
pkgrel=1

pkgdesc='paragraph reformatter'
url='http://www.nicemice.net/par/'
arch=('i686' 'x86_64')
license=('custom')

depends=('glibc')

source=("http://www.nicemice.net/par/Par${pkgver/./}.tar.gz")

sha1sums=('4b83d2ec593bb45ee46d4b7c2bfc590e1f4a41a8')

prepare() {
    cd Par"${pkgver/./}"
    sed -n '67,85p' par.doc > LICENSE
}

build() {
    cd Par"${pkgver/./}"
    make -f protoMakefile
}

package() {
    cd Par"${pkgver/./}"
    install -Dm0755 par "$pkgdir"/usr/bin/par
    install -Dm0644 par.1 "$pkgdir"/usr/share/man/man1/par.1
    install -Dm0644 LICENSE "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE
}
