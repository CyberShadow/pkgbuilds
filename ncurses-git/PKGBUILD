pkgname=ncurses-git
pkgver=6.0.r4.ga3725e3
pkgrel=1

pkgdesc='Unofficial git mirror of snapshots from ftp://invisible-island.net/ncurses/current/'
url='http://ncurses.scripts.mit.edu/?p=ncurses.git'
arch=('i686' 'x86_64')
license=('MIT')

depends=('glibc' 'gcc-libs')
makedepends=('git')

provides=('ncurses')
conflicts=('ncurses')

source=('git://ncurses.scripts.mit.edu/ncurses.git')

md5sums=('SKIP')

pkgver() {
    cd ncurses
    git describe --tags | sed 's/^v//; s/-/.r/; s/-/./'
}

build() {
    CFLAGS="${CFLAGS/-flto/}"

    cd ncurses
    ./configure --prefix=/usr \
        --with-shared \
        --with-normal \
        --without-ada  \
        --with-pthread \
        --enable-widec \
        --without-debug \
        --enable-pc-files \
        --with-cxx-shared \
        --with-cxx-binding \
        --enable-hard-tabs \
        --enable-weak-symbols
    make
}

package() {
    cd ncurses
    make DESTDIR="$pkgdir" install

    # Fool packages looking to link to non-wide-character (n)curses libraries.
    for lib in ncurses form panel menu; do
        printf 'INPUT(-l%sw)\n' "$lib" > "$pkgdir"/usr/lib/lib"$lib".so
        ln -s "$lib"w.pc "$pkgdir"/usr/share/pkgconfig/"$lib".pc
    done

    printf 'INPUT(-lncursesw)\n' > "$pkgdir"/usr/lib/libcursesw.so
    ln -s libncurses.so "$pkgdir"/usr/lib/libcurses.so

    # Include some introduction and informal guides as they're quite nicely written.
    install -Dm644 doc/hackguide.doc "$pkgdir"/usr/share/doc/ncurses/hackguide.doc
    install -Dm644 doc/ncurses-intro.doc "$pkgdir"/usr/share/doc/ncurses/ncurses-intro.doc

    install -Dm755 COPYING "$pkgdir"/usr/share/licenses/"$pkgname"/COPYING
} 
