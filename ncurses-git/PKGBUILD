pkgname=ncurses-git
pkgver=5.9.r135.g2e5d72d
pkgrel=1

pkgdesc="Unofficial git mirror of snapshots from ftp://invisible-island.net/ncurses/current/"
arch=('i686' 'x86_64')
url="http://ncurses.scripts.mit.edu/?p=ncurses.git"
license=('MIT')

depends=('glibc')
makedepends=('git')

provides=('ncurses')
replaces=('ncurses')
conflicts=('ncurses')

source=('git://ncurses.scripts.mit.edu/ncurses.git')
md5sums=('SKIP')

pkgver() {
    cd ncurses
    git describe --tags | sed 's/^v//; s/-/.r/; s/-/./'
}

build() {
    mkdir ncurses-build
    mkdir ncursesw-build
    cd ncursesw-build
    # add --enable-ext-colors and --enable-ext-mouse with next soname bump
    configopts=(
        --prefix=/usr 
        --mandir=/usr/share/man
        --with-shared
        --with-normal
        --without-debug
        --without-ada 
        --enable-widec
        --enable-pc-files
    )
    ../ncurses/configure "${configopts[@]}"
    make

    # libraries for external binary support 
    cd "$srcdir"/ncurses-build
    if [[ "$CARCH" = x86_64 ]]; then
        config_chtype_long='--with-chtype=long'
    fi

    configopts=(
        --prefix=/usr 
        --with-shared
        --with-normal
        --without-debug
        --without-ada 
        "$config_chtype_long"
    )
    ../ncurses/configure "${configopts[@]}"
    make
}

package() {
    cd ncursesw-build
    make DESTDIR="$pkgdir" install

    # fool packages looking to link to non-wide-character ncurses libraries
    for lib in ncurses form panel menu; do
        printf "INPUT(-l%sw)\n" "$lib" > "$pkgdir"/usr/lib/lib"$lib".so
    done

    for lib in ncurses ncurses++ form panel menu; do
        ln -s "$lib"w.pc "$pkgdir"/usr/lib/pkgconfig/"$lib".pc
    done

    # some packages look for -lcurses during build
    printf "INPUT(-lncursesw)\n" > "$pkgdir"/usr/lib/libcursesw.so
    ln -s libncurses.so "$pkgdir"/usr/lib/libcurses.so

    # non-widec compatibility libraries
    cd "$srcdir"/ncurses-build
    for lib in ncurses form panel menu; do
        install -Dm755 lib/lib"$lib".so.5.9 "$pkgdir"/usr/lib/lib"$lib".so.5.9
        ln -s lib"$lib".so.5.9 "$pkgdir"/usr/lib/lib"$lib".so.5
    done

    cd "$srcdir"/ncurses
    install -Dm755 COPYING "$pkgdir"/usr/share/licenses/ncurses/COPYING
} 
