pkgname=ncurses-git
pkgver=6.0.r60.g16fea43
pkgrel=1

pkgdesc='unofficial snapshots from ftp://invisible-island.net/ncurses/current/'
url='http://ncurses.scripts.mit.edu/?p=ncurses.git'
arch=('i686' 'x86_64')
license=('MIT')

depends=('glibc' 'gcc-libs')
makedepends=('git')

# Library provide now needed for Arch Linux's bash package, sigh.
provides=('ncurses' 'libtinfo' 'libncursesw.so')
conflicts=('ncurses' 'libtinfo')

source=('git://ncurses.scripts.mit.edu/ncurses.git')

sha1sums=('SKIP')

pkgver() {
    cd ncurses
    git describe --tags | sed 's/^v//; s/-/.r/; s/-/./'
}

build() {
    cd ncurses
    ./configure --prefix=/usr \
        --with-shared \
        --with-normal \
        --without-ada  \
        --enable-widec \
        --without-debug \
        --enable-pthread \
        --enable-pc-files \
        --with-cxx-shared \
        --with-cxx-binding \
        --enable-hard-tabs \
        --enable-weak-symbols
    make
}

package() {
    cd ncurses
    make DESTDIR="$pkgdir" install

    # Fool packages looking to link to non-wide-character (n)curses libraries.
    for lib in ncurses ncurses++ form panel menu; do
        printf 'INPUT(-l%sw)\n' "$lib" > "$pkgdir"/usr/lib/lib"$lib".so
        ln -s "$lib"w.pc "$pkgdir"/usr/share/pkgconfig/"$lib".pc
    done

    printf 'INPUT(-lncursesw)\n' > "$pkgdir"/usr/lib/libcursesw.so
    ln -sf libncurses.so "$pkgdir"/usr/lib/libcurses.so

    # Include some introduction and informal guides as they're quite nicely written.
    install -Dm644 doc/hackguide.doc "$pkgdir"/usr/share/doc/"$pkgname"/hackguide.doc
    install -Dm644 doc/ncurses-intro.doc "$pkgdir"/usr/share/doc/"$pkgname"/ncurses-intro.doc

    install -Dm755 COPYING "$pkgdir"/usr/share/licenses/"$pkgname"/COPYING
}
