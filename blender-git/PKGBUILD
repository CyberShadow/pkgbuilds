pkgname=blender-git
pkgver=2.74.rc12999.g32578db
pkgrel=1

pkgdesc='integrated 3d graphics creation and animation'
url='http://blender.org/'
arch=('i686' 'x86_64')
license=('GPL')

depends=('libgl' 'python' 'desktop-file-utils' 'hicolor-icon-theme' 'sdl'
        'ffmpeg' 'fftw' 'openal' 'freetype2' 'libxi' 'openimageio' 'opencolorio'
        'openshadinglanguage' 'libtiff' 'libpng' 'libjpeg-turbo' 'boost-libs'
        'libsndfile' 'openexr')

makedepends=('git' 'cmake' 'boost' 'mesa' 'llvm')
makedepends_x86_64=('cuda')

optdepends=('cuda: cycles renderer support')

provides=('blender')
conflicts=('blender')

source=('git://git.blender.org/blender.git'
        'git://git.blender.org/blender-addons.git'
        'git://git.blender.org/blender-addons-contrib.git'
        'git://git.blender.org/blender-translations.git'
        'blender.desktop')

sha1sums=('SKIP'
          'SKIP'
          'SKIP'
          'SKIP'
          '590661ee80756386358f1031934d071895283d28')

pkgver() {
    cd blender
    git describe | sed 's/^v//; s/-/./; s/-//; s/-/./'
}

prepare() {
    cd blender
    git submodule init
    git config submodule.release/scripts/addons.url "$srcdir"/blender-addons
    git config submodule.release/scripts/addons_contrib.url "$srcdir"/blender-addons-contrib
    git config submodule.release/datafiles/locale.url "$srcdir"/blender-translations
    git submodule update release/scripts/addons release/scripts/addons_contrib release/datafiles/locale
    mkdir build
}

build() {
    # Blender won't let us build in the source tree directly.
    cd blender/build
    
    if [[ $CARCH == i686 ]]; then
        _cuda=OFF
    else
        _cuda=ON
    fi

    _llvm=$(llvm-config --version)
    _python=$(python -c 'import sys; print("{}.{}m".format(*sys.version_info))')

    cmake "$srcdir"/blender \
          -DWITH_LLVM=ON \
          -DWITH_FFTW3=ON \
          -DWITH_PLAYER=ON \
          -DWITH_CYCLES=ON \
          -DLLVM_STATIC=OFF \
          -DWITH_CYCLES_OSL=ON \
          -DWITH_GAMEENGINE=ON \
          -DWITH_SYSTEM_GLEW=ON \
          -DWITH_OPENCOLLADA=ON \
          -DWITH_CODEC_FFMPEG=ON \
          -DWITH_MOD_OCEANSIM=ON \
          -DWITH_CODEC_FFMPEG=ON \
          -DWITH_CODEC_SNDFILE=ON \
          -DLLVM_VERSION="$_llvm" \
          -DPYTHON_LIBPATH=/usr/lib \
          -DWITH_PYTHON_INSTALL=OFF \
          -DCMAKE_INSTALL_PREFIX=/usr \
          -DOPENIMAGEIO_ROOT_DIR=/usr \
          -DWITH_INSTALL_PORTABLE=OFF \
          -DPYTHON_VERSION="$_python" \
          -DCMAKE_BUILD_TYPE=RelWithDebInfo \
          -DPYTHON_LIBRARY=python"$_python"m \
          -DWITH_CYCLES_CUDA_BINARIES="$_cuda" \
          -DPYTHON_INCLUDE_DIRS=/usr/include/python"$_python"m
    make
}

package() {
    cd blender/build
    make DESTDIR="$pkgdir" install

    # Track the .pyc files.
    python -Om compileall "$pkgdir"/usr/share/blender
}
