pkgname=bemenu-git
pkgver=r243.d626127
pkgrel=1

pkgdesc='dynamic menu library and client program inspired by dmenu'
url='https://github.com/Cloudef/bemenu'
arch=('i686' 'x86_64')
license=('GPL3' 'LGPL3')

depends=('pango')
makedepends=('git' 'cmake' 'libxkbcommon' 'libxinerama' 'wayland')
optdepends=('wayland: wayland support'
            'libxkbcommon: wayland support'
            'libxinerama: x11 support'
            'ncurses: curses support')

provides=('bemenu')
conflicts=('bemenu')

source=('git+https://github.com/Cloudef/bemenu')

sha1sums=('SKIP')

pkgver() {
    cd bemenu
    printf 'r%s.g%s' "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd bemenu
    cmake -DCMAKE_BUILD_TYPE=RelWithDebInfo -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib
    make
}

check() {
    cd bemenu
    make test
}

package() {
    cd bemenu
    make DESTDIR="$pkgdir" install
}
