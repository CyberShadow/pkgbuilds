pkgname=rtmpdump-git
pkgver=r498.a1900c3
pkgrel=1

pkgdesc='A tool to download rtmp:// and rtmpe:// streams.'
url='http://rtmpdump.mplayerhq.hu/'
arch=('i686' 'x86_64')
license=('GPL2' 'LGPL2.1')

depends=('openssl')
makedepends=('git' 'zlib')

provides=('rtmpdump')
conflicts=('rtmpdump')

source=('git://git.ffmpeg.org/rtmpdump')

md5sums=('SKIP')

pkgver() {
    cd rtmpdump
    printf 'r%s.%s' "$(git rev-list --count HEAD)" "$(git describe --always)"
}

build() {
    cd rtmpdump
    make
}

package() {
    cd rtmpdump
    make -j1 prefix=/usr mandir=/usr/share/man DESTDIR="$pkgdir" install

    mv "$pkgdir"/usr/sbin/* "$pkgdir"/usr/bin
    rmdir "$pkgdir"/usr/sbin
}
