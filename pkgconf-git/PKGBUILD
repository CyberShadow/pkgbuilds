pkgname=pkgconf-git
pkgver=1.3.7.r33.g9505213
pkgrel=2

pkgdesc='compiler and linker configuration'
url='https://github.com/pkgconf/pkgconf'
arch=('i686' 'x86_64')
license=('ISC')

depends=('glibc')
makedepends=('popt' 'git')

conflicts=('pkgconfig' 'pkgconf')
provides=('pkgconfig' 'pkgconf')

source=('git+https://github.com/pkgconf/pkgconf')
sha1sums=('SKIP')

pkgver() {
    cd pkgconf
    git describe --long --tags | sed 's/^pkgconf-//; s/-/.r/; s/-/./'
}

build() {
    cd pkgconf
    autoreconf -fi
    ./configure --prefix=/usr
    make
}

package() {
    cd pkgconf
    make DESTDIR="$pkgdir" install
    install -Dm0644 COPYING "$pkgdir"/usr/share/licenses/"$pkgname"/COPYING

    # Provide this as a drop-in replacement for pkg-config.
    ln -s /usr/bin/pkgconf "$pkgdir"/usr/bin/pkg-config
}
