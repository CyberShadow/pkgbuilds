pkgbase=system-config
pkgname=(base-config teapot-config engine-config)
pkgver=6.0
pkgrel=1

pkgdesc='system configurations'
arch=('any')
url='https://github.com/Earnestly'
license=('custom')

source=('env-nvidia.sh'
        'etc-zshenv'
        'etc-locale.conf'
        'etc-vconsole.conf'
        'mod-i915.conf'
        'mod-pcspkr.conf'
        'mod-usbhid.conf'
        'mod-uvcvideo.conf'
        'nft-nftables.conf'
        'sudo-wheel'
        'sysctl-swap.conf'
        'sysctl-dirty.conf'
        'sysctl-fq_codel.conf'
        'xorg-trackpoint.conf'
        'xorg-libinput-tweaks.conf'
        'xorg-libinput-noaccel.conf'
        'xorg-synaptics-tweaks.conf'
        'hwdb-capslock-as-control.hwdb')

package_base-config() {
    pkgdesc='common system configuration'
    depends=('zsh' 'sudo')

    install -Dm0644 etc-zshenv "$pkgdir"/etc/zsh/zshenv
    install -Dm0644 etc-locale.conf "$pkgdir"/etc/locale.conf
    install -Dm0644 etc-vconsole.conf "$pkgdir"/etc/vconsole.conf
    install -Dm0644 mod-pcspkr.conf "$pkgdir"/etc/modprobe.d/pcspkr.conf
    install -Dm0644 mod-usbhid.conf "$pkgdir"/etc/modprobe.d/usbhid.conf
    install -Dm0600 sudo-wheel "$pkgdir"/etc/sudoers.d/wheel
    install -Dm0644 sysctl-dirty.conf "$pkgdir"/etc/sysctl.d/dirty.conf
    install -Dm0644 sysctl-fq_codel.conf "$pkgdir"/etc/sysctl.d/fq_codel.conf
    ln -sf /usr/share/zoneinfo/Europe/London "$pkgdir"/etc/localtime
}

package_teapot-config() {
    pkgdesc='system configurations for teapot'
    depends=('base-config' 'xf86-input-libinput' 'xf86-input-synaptics'
             'nftables')

    install -Dm0644 mod-i915.conf "$pkgdir"/etc/modprobe.d/i915.conf
    install -Dm0644 nft-nftables.conf "$pkgdir"/etc/nftables.conf
    install -Dm0644 sysctl-swap.conf "$pkgdir"/etc/sysctl.d/swap.conf
    install -Dm0644 xorg-trackpoint.conf "$pkgdir"/etc/X11/xorg.conf.d/13-trackpoint.conf
    install -Dm0644 xorg-libinput-tweaks.conf "$pkgdir"/etc/X11/xorg.conf.d/11-libinput-tweaks.conf
    install -Dm0644 xorg-synaptics-tweaks.conf "$pkgdir"/etc/X11/xorg.conf.d/51-synaptics-tweaks.conf
    install -Dm0644 hwdb-capslock-as-control.hwdb "$pkgdir"/etc/udev/hwdb.d/61-capslock-as-control.hwdb

}

package_engine-config() {
    pkgdesc='system configurations for engine'
    depends=('base-config' 'xf86-input-libinput' 'xf86-input-wacom' 'nvidia'
             'nvidia-utils' 'nvidia-settings')

    install -Dm0644 env-nvidia.sh "$pkgdir"/etc/profile.d/nvidia.sh
    install -Dm0644 xorg-libinput-tweaks.conf "$pkgdir"/etc/X11/xorg.conf.d/11-libinput-tweaks.conf
    install -Dm0644 xorg-libinput-noaccel.conf "$pkgdir"/etc/X11/xorg.conf.d/12-libinput-noaccel.conf
}

sha1sums=('d5b9bc75719e4bdeeff0ed13d09c74382d0ffa3b'
          '2e5becba8c1815ddcb4aa03f28d6e015f85f115c'
          '31ff548797814ab1cf842fe3ed86e7b8c66c4be4'
          'c43c8c01db104b1f5747b23fda21700819ad67fb'
          '5b59530577e11ace46fa5df59f8782f82ffe148d'
          'ee919734ce53dcebbe319f230aca847f86e2249c'
          '7548bbe488a8b02cf47e365eb3eca35a8eb5f3da'
          '3b3a6f07f7934ca435deb853cd607506eda4fb1c'
          '1b677e55c9a74777a190e4cd504323d2b093fc08'
          '9ab5618b9c186d99e3ba8a88272761f34ff4b00c'
          '929b10c4bb2c3414458420ad86ed32be47723769'
          '430e8a142b7c9f1c78b3a70ca3370302c350e950'
          '94e3346d3e98da1ec62bcd07901894893bd9b930'
          'd2323a2ca403ffa7cb5bd4e7dd68d1ab1a49bfce'
          'b2f3e8baae07dc0dc972ed018a75b3adb73bbd46'
          '0d5b92f3fae2fbc0d523617714218cad782449b9'
          'ebf354ca8f185b9afb579731e2b23a0c767e20a9'
          '07d9adb18380d212fe886894ed620c90cdc6cf2e')
