pkgbase=system-config
pkgname=(base-config teapot-config engine-config)
pkgver=2.6
pkgrel=1

pkgdesc='system configurations'
arch=('any')
url='https://github.com/Earnestly'
license=('custom')

source=('env-nvidia.sh'
        'etc-zshenv'
        'etc-locale.conf'
        'etc-vconsole.conf'
        'mod-i915.conf'
        'mod-pcspkr.conf'
        'mod-uvcvideo.conf'
        'nft-nftables.conf'
        'sudo-wheel'
        'sysctl-swap.conf'
        'sysctl-dirty.conf'
        'sysctl-fq_codel.conf'
        'xorg-intel.conf'
        'xorg-trackpoint.conf'
        'xorg-libinput-tweaks.conf'
        'xorg-libinput-noaccel.conf'
        'xorg-synaptics-tweaks.conf'
        'udev-psmouse-sensitivity.rules')

package_base-config() {
    pkgdesc='common system configuration'
    depends=('zsh' 'sudo')

    install -Dm644 etc-zshenv "$pkgdir"/etc/zsh/zshenv
    install -Dm644 etc-locale.conf "$pkgdir"/etc/locale.conf
    install -Dm644 etc-vconsole.conf "$pkgdir"/etc/vconsole.conf
    install -Dm644 mod-pcspkr.conf "$pkgdir"/etc/modprobe.d/pcspkr.conf
    install -Dm644 sudo-wheel "$pkgdir"/etc/sudoers.d/wheel
    install -Dm644 sysctl-dirty.conf "$pkgdir"/etc/sysctl.d/dirty.conf
    install -Dm644 sysctl-fq_codel.conf "$pkgdir"/etc/sysctl.d/fq_codel.conf
    ln -sf /usr/share/zoneinfo/Europe/London "$pkgdir"/etc/localtime
}

package_teapot-config() {
    pkgdesc='system configurations for teapot'
    depends=('base-config' 'xf86-input-libinput' 'xf86-input-synaptics'
             'xf86-video-intel' 'nftables')

    install -Dm644 mod-i915.conf "$pkgdir"/etc/modprobe.d/i915.conf
    install -Dm644 nft-nftables.conf "$pkgdir"/etc/nftables.conf
    install -Dm644 sysctl-swap.conf "$pkgdir"/etc/sysctl.d/swap.conf
    install -Dm644 xorg-intel.conf "$pkgdir"/etc/X11/xorg.conf.d/12-intel.conf
    install -Dm644 xorg-trackpoint.conf "$pkgdir"/etc/X11/xorg.conf.d/13-trackpoint.conf
    install -Dm644 xorg-libinput-tweaks.conf "$pkgdir"/etc/X11/xorg.conf.d/11-libinput-tweaks.conf
    install -Dm644 xorg-synaptics-tweaks.conf "$pkgdir"/etc/X11/xorg.conf.d/51-synaptics-tweaks.conf

    # <https://bugzilla.redhat.com/show_bug.cgi?id=1200717>
    install -Dm644 udev-psmouse-sensitivity.rules "$pkgdir"/etc/udev/rules.d/99-psmouse-sensitivity.rules
}

package_engine-config() {
    pkgdesc='system configurations for engine'
    depends=('base-config' 'xf86-input-libinput' 'xf86-input-wacom' 'nvidia'
             'nvidia-utils' 'nvidia-settings')

    install -Dm644 env-nvidia.sh "$pkgdir"/etc/profile.d/nvidia.sh
    install -Dm644 xorg-libinput-tweaks.conf "$pkgdir"/etc/X11/xorg.conf.d/11-libinput-tweaks.conf
    install -Dm644 xorg-libinput-noaccel.conf "$pkgdir"/etc/X11/xorg.conf.d/11-libinput-noaccel.conf
}

sha1sums=('d5b9bc75719e4bdeeff0ed13d09c74382d0ffa3b'
          '2e5becba8c1815ddcb4aa03f28d6e015f85f115c'
          '31ff548797814ab1cf842fe3ed86e7b8c66c4be4'
          'c43c8c01db104b1f5747b23fda21700819ad67fb'
          '5b59530577e11ace46fa5df59f8782f82ffe148d'
          'ee919734ce53dcebbe319f230aca847f86e2249c'
          '3b3a6f07f7934ca435deb853cd607506eda4fb1c'
          '1b677e55c9a74777a190e4cd504323d2b093fc08'
          '7efaee60412cce9275e2fbfa6e8b967c6c504621'
          '929b10c4bb2c3414458420ad86ed32be47723769'
          'b8518cddebd8e5c2c034b622887ebd1ec4f54602'
          '94e3346d3e98da1ec62bcd07901894893bd9b930'
          'c96f4a2a54797fe7a3c3f590d68a929d3b2cde53'
          'd2323a2ca403ffa7cb5bd4e7dd68d1ab1a49bfce'
          'b2f3e8baae07dc0dc972ed018a75b3adb73bbd46'
          '0d5b92f3fae2fbc0d523617714218cad782449b9'
          'ebf354ca8f185b9afb579731e2b23a0c767e20a9'
          '5e1080259c59fe9b884e4b2232fdb58debda272e')
