pkgbase=system-config
pkgname=(base-config teapot-config engine-config)
pkgver=7.1
pkgrel=1

pkgdesc='system configurations'
arch=('any')
url='https://github.com/Earnestly'
license=('custom')

source=('env-nvidia.sh'
        'etc-zshenv'
        'etc-locale.conf'
        'etc-vconsole.conf'
        'hwdb-caps-as-ctrl-holtek-generic.hwdb'
        'hwdb-caps-as-ctrl-thinkpad.hwdb'
        'locale-en_GB'
        'mod-i915.conf'
        'mod-pcspkr.conf'
        'mod-usbhid.conf'
        'mod-uvcvideo.conf'
        'nft-nftables.conf'
        'sudo-wheel'
        'sysctl-swap.conf'
        'sysctl-dirty.conf'
        'sysctl-fq_codel.conf'
        'xorg-trackpoint.conf'
        'xorg-libinput-tweaks.conf'
        'xorg-libinput-noaccel.conf'
        'xorg-synaptics-tweaks.conf')


package_base-config() {
    pkgdesc='common system configuration'
    depends=('zsh' 'sudo')

    install -Dm0644 etc-zshenv "$pkgdir"/etc/zsh/zshenv
    install -Dm0644 etc-locale.conf "$pkgdir"/etc/locale.conf
    install -Dm0644 etc-vconsole.conf "$pkgdir"/etc/vconsole.conf
    install -Dm0644 locale-en_GB "$pkgdir"/etc/locales/en_GB
    install -Dm0644 mod-pcspkr.conf "$pkgdir"/etc/modprobe.d/pcspkr.conf
    install -Dm0644 mod-usbhid.conf "$pkgdir"/etc/modprobe.d/usbhid.conf
    install -Dm0600 sudo-wheel "$pkgdir"/etc/sudoers.d/wheel
    install -Dm0644 sysctl-dirty.conf "$pkgdir"/etc/sysctl.d/dirty.conf
    install -Dm0644 sysctl-fq_codel.conf "$pkgdir"/etc/sysctl.d/fq_codel.conf
    ln -sf /usr/share/zoneinfo/Europe/London "$pkgdir"/etc/localtime
}

package_teapot-config() {
    pkgdesc='system configurations for teapot'
    depends=('base-config' 'xf86-input-libinput' 'xf86-input-synaptics'
             'nftables')

    install -Dm0644 mod-i915.conf "$pkgdir"/etc/modprobe.d/i915.conf
    install -Dm0644 nft-nftables.conf "$pkgdir"/etc/nftables.conf
    install -Dm0644 sysctl-swap.conf "$pkgdir"/etc/sysctl.d/swap.conf
    install -Dm0644 xorg-trackpoint.conf "$pkgdir"/etc/X11/xorg.conf.d/13-trackpoint.conf
    install -Dm0644 xorg-libinput-tweaks.conf "$pkgdir"/etc/X11/xorg.conf.d/11-libinput-tweaks.conf
    install -Dm0644 xorg-synaptics-tweaks.conf "$pkgdir"/etc/X11/xorg.conf.d/51-synaptics-tweaks.conf
    install -Dm0644 hwdb-caps-as-ctrl-thinkpad.hwdb "$pkgdir"/etc/udev/hwdb.d/61-caps-as-ctrl-thinkpad.hwdb
}

package_engine-config() {
    pkgdesc='system configurations for engine'
    depends=('base-config' 'xf86-input-libinput' 'xf86-input-wacom' 'nvidia'
             'nvidia-utils' 'nvidia-settings')

    install -Dm0644 env-nvidia.sh "$pkgdir"/etc/profile.d/nvidia.sh
    install -Dm0644 xorg-libinput-tweaks.conf "$pkgdir"/etc/X11/xorg.conf.d/11-libinput-tweaks.conf
    install -Dm0644 xorg-libinput-noaccel.conf "$pkgdir"/etc/X11/xorg.conf.d/12-libinput-noaccel.conf
    install -Dm0644 hwdb-caps-as-ctrl-holtek-generic.hwdb "$pkgdir"/etc/udev/hwdb.d/61-caps-as-ctrl-holtek-generic.hwdb
}

sha256sums=('c88726acdb7b2a7d51f8c3ada4c8dabb23ab75f7c82d64516551f267da37bd21'
            'f6f0c7a9cf710c0c2722c3f42d9eca690c8206bfe4660d33f99e321de52e9cce'
            '337ad308b494e9d3261fc6b75fdd450917a2b14930f675824acf358a2dadc815'
            '2c09b43e51de87a82ff5eeb1c599ce25800b8d34810f14044bf73644e9f7b742'
            'f18fb2629138504be8631f365d44f5de37e02e2eaa2d862fcdeed3df9350c571'
            '14a5257880b3d92aa018299d54e97bd356732aa22a24ca9f3db61a7d8dadc4a4'
            'e3b0c44298fc1c149afbf4c8996fb92427ae41e4649b934ca495991b7852b855'
            '81733f1de38b8421aaa5bc3b3f4274490c1fd8ba5343b4aab7ec73a89cfda0af'
            'e30f05181fa754a9b1071ef48bf50f0296d9cbe16a8ba097a95df40506af8ce3'
            'f83b630e1fd67c37090932bd48154ba9f356c4fa519ddeca75b1f97f2bba3f73'
            'f17d620ed2de0d5b568b6eb389f6c55852e615c8b26772e404242cbf3b857f41'
            'bf9d6ba62e41cc317476c496e273d766209c1ff4fe4b0353ecf6c92f41d11568'
            '1d6e65e7dcf0af1a341a414d5659b5feaf5e5d9b3e53334d531d8ec7e52be5a4'
            '0c164df1a0702723c2e2a48fe7224b2e7318cd83253126c44293ff4c9ff0ba2e'
            'f3ac45e8e1c835886b3daa647033c54e889acc8766c9fbd7bccb80a006438f5d'
            '6a3a3d076b01d99693edba864270ce85e125fed853ac79fe32827c499cdccbe6'
            'fddca78e68e9454a45c0b39bc07ee18bdc9ad34db49ec63bf102816ed18e9c65'
            'def18791c8aba7165603c48072be3b431212ed0dc67cf441833714c6858cfc25'
            'a2e65efdd328d1952692198667140aeb1be6269ba0f238c6d7970783c051348a'
            '21f96ad3faa793f6b2f7659ad76432dce272e1e9794a5871bdbdf401bb76e287')
