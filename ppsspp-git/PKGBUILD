pkgname=ppsspp-git
pkgver=1.0.1.r830.gc671b07
pkgrel=1

pkgdesc='A PSP emulator for Android, Windows, Mac, and Linux, written in C++.'
url='http://www.ppsspp.org/'
arch=('i686' 'x86_64')
license=('BSD' 'GPL2')

depends=('sdl2' 'ffmpeg' 'xdg-utils')
makedepends=('git' 'cmake' 'clang')

provides=('ppsspp')
conflicts=('ppsspp')

source=('git://github.com/hrydgard/ppsspp.git'
        'git://github.com/hrydgard/native.git'
        'git://github.com/hrydgard/ppsspp-lang.git'
        'git://github.com/Kingcom/armips')

md5sums=('SKIP' 'SKIP' 'SKIP' 'SKIP')

install='ppsspp-git.install'

pkgver() {
    cd ppsspp
    git describe | sed 's/^v//; s/-/.r/; s/-/./'
}

prepare() {
    cd ppsspp
    git submodule init
    git config submodule.native.url "$srcdir"/native
    git config submodule.lang.url "$srcdir"/ppsspp-lang
    git config submodule.ext/armips.url "$srcdir"/armips
    git submodule update native lang ext/armips
}

build() {
    cd ppsspp
    cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DUSE_SYSTEM_FFMPEG=ON
    make
}

package() {
    cd ppsspp
    mkdir -p "$pkgdir"/usr/share/{icons,ppsspp}
    mv assets/unix-icons/hicolor "$pkgdir"/usr/share/icons

    chmod -x assets/{icon-114.png,icon.svg}
    cp -a assets/* "$pkgdir"/usr/share/ppsspp

    mkdir -p "$pkgdir"/usr/bin
    install -Dm755 PPSSPPSDL "$pkgdir"/usr/share/ppsspp/PPSSPPSDL
    ln -sf /usr/share/ppsspp/PPSSPPSDL "$pkgdir"/usr/bin/ppsspp

    install -Dm644 debian/ppsspp.1 "$pkgdir"/usr/share/man/man1/ppsspp.1
    install -Dm644 debian/ppsspp.desktop "$pkgdir"/usr/share/applications/ppsspp.desktop
    install -Dm644 LICENSE.TXT "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE
}
