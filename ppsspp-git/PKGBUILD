pkgname=ppsspp-git
pkgver=0.9.9.1.r1195.ga346b70
pkgrel=1

pkgdesc='A PSP emulator for Android, Windows, Mac, and Linux, written in C++.'
url='http://www.ppsspp.org/'
arch=('i686' 'x86_64')
license=('BSD' 'GPL2')

depends=('zlib' 'sdl' 'ffmpeg')
makedepends=('git' 'cmake')

provides=('ppsspp')
conflicts=('ppsspp')

source=('git://github.com/hrydgard/ppsspp.git'
        'git://github.com/hrydgard/native.git'
        'git://github.com/Kingcom/armips'
        'git://github.com/hrydgard/ppsspp-lang.git')
        #'git://github.com/hrydgard/ppsspp-ffmpeg.git')

md5sums=('SKIP' 'SKIP' 'SKIP' 'SKIP')

pkgver() {
    cd ppsspp
    git describe | sed 's/^v//; s/-/.r/; s/-/./'
}

prepare() {
    cd ppsspp
    git submodule init
    git config submodule.native.url "$srcdir"/native
    git config submodule.lang.url "$srcdir"/ppsspp-lang
    git config submodule.ext/armips.url "$srcdir"/armips
    #git config submodule.ffmpeg.url "$srcdir"/ppsspp-ffmpeg
    git submodule update native lang ext/armips #ffmpeg
}

build() {
    cd ppsspp
    export LDFLAGS="-lavutil -lavformat -lavcodec -lswscale -lswresample"
    cmake -DUSING_EGL=ON -DUSING_GLES2=ON
    make
}

package() {
    cd ppsspp

    # There is no `make install` yet
    install -dm755 "$pkgdir"/usr/share/ppsspp
    cp -a PPSSPPSDL assets/* "$pkgdir"/usr/share/ppsspp

    # Tidy up, remove executable permission from various files
    chmod -x "$pkgdir"/usr/share/ppsspp/{icon.svg,icon-114.png}

    # Install this funky license
    install -Dm644 LICENSE.TXT "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE

    # Symlink to /usr/bin/ppsspp
    install -dm755 "$pkgdir"/usr/bin
    ln -sf /usr/share/ppsspp/PPSSPPSDL "$pkgdir"/usr/bin/ppsspp
}
