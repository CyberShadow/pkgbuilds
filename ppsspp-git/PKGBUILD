pkgname=ppsspp-git
pkgver=1.1.1.r324.gce05930
pkgrel=1

pkgdesc='A PSP emulator'
url='http://www.ppsspp.org/'
arch=('i686' 'x86_64')
license=('BSD' 'GPL2')

depends=('sdl2' 'ffmpeg' 'glew' 'snappy')
makedepends=('git' 'cmake')

provides=('ppsspp')
conflicts=('ppsspp')

source=('git+https://github.com/hrydgard/ppsspp'
        'git+https://github.com/hrydgard/ppsspp-lang'
        'git+https://github.com/Kingcom/armips')

sha1sums=('SKIP' 'SKIP' 'SKIP')

pkgver() {
    cd ppsspp
    git describe | sed 's/^v//; s/-/.r/; s/-/./'
}

prepare() {
    cd ppsspp
    git submodule init
    git config submodule.lang.url "$srcdir"/ppsspp-lang
    git config submodule.ext/armips.url "$srcdir"/armips
    git submodule update lang ext/armips
}

build() {
    cd ppsspp
    cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=RelWithDebInfo -DUSE_SYSTEM_FFMPEG=ON
    make
}

package() {
    cd ppsspp

    install -dm755 "$pkgdir"/usr/bin
    install -Dm755 PPSSPPSDL "$pkgdir"/usr/share/ppsspp/PPSSPPSDL
    install -Dm644 LICENSE.TXT "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE

    ln -sf /usr/share/ppsspp/PPSSPPSDL "$pkgdir"/usr/bin/ppsspp

    # XXX Icon generation doesn't appear to work
    # cd assets/unix-icons
    # ./convert-to-png.sh -s imagemagick -d "$pkgdir"/usr/share/icons/hicolor 

}
