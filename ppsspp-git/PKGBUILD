pkgname=ppsspp-git
pkgver=0.9.6.r700.g5e4a076
pkgrel=1

pkgdesc='A PSP emulator for Android, Windows, Mac, and Linux, written in C++.'
arch=('i686' 'x86_64')
url='http://www.ppsspp.org/'
license=('GPL2')

depends=('zlib' 'sdl' 'ffmpeg')
makedepends=('git' 'cmake')

provides=('ppsspp')
conflicts=('ppsspp')

source=('git://github.com/hrydgard/ppsspp.git'
        'git://github.com/hrydgard/native.git'
        'git://github.com/hrydgard/ppsspp-lang.git'
        'git://github.com/hrydgard/ppsspp-ffmpeg.git')

md5sums=('SKIP' 'SKIP' 'SKIP' 'SKIP')

pkgver() {
    cd ppsspp
    git describe | sed 's/^v//; s/-/.r/; s/-/./'
}

prepare() {
    cd ppsspp
    git submodule init
    git config submodule.native.url "$srcdir"/native
    git config submodule.lang.url "$srcdir"/ppsspp-lang
    git config submodule.ffmpeg.url "$srcdir"/ppsspp-ffmpeg
    git submodule update native lang ffmpeg
}

build() {
    cd ppsspp
    cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release
    make
}

package() {
    cd ppsspp

    # There is no `make install` yet
    mkdir -p "$pkgdir"/usr/share/ppsspp
    cp -a PPSSPPSDL assets/* "$pkgdir"/usr/share/ppsspp

    # Tidy up
    chmod -x "$pkgdir"/usr/share/ppsspp/{icon.svg,icon-114.png}

    # Symlink to /usr/bin/ppsspp
    mkdir -p "$pkgdir"/usr/bin
    ln -sf /usr/share/ppsspp/PPSSPPSDL "$pkgdir"/usr/bin/ppsspp
}

