pkgname=retroarch-git
pkgver=1.3.1.r248.g9459cf6
pkgrel=1

pkgdesc='Simple frontend for the libretro API'
arch=('i686' 'x86_64')
url='http://github.com/libretro/RetroArch'
license=('GPL')

depends=('mesa' 'sdl' 'nvidia-cg-toolkit')
makedepends=('git')
optdepends=('python: For converting cg shaders to glsl shaders')

provides=('retroarch')
conflicts=('retroarch')

source=('git+https://github.com/libretro/RetroArch.git')

sha1sums=('SKIP')

pkgver() {
    cd RetroArch
    git describe --tags | sed 's/^v//; s/-/.r/; s/-/./'
}

build() {
    cd RetroArch
    ./configure --prefix=/usr \
        --enable-cg \
        --enable-kms \
        --enable-udev \
        --enable-sdl2 \
        --enable-pulse \
        --enable-libxml2 \
        --enable-threads \
        --enable-freetype
    make
}

package() {
    cd RetroArch
    make DESTDIR="$pkgdir" install
}
