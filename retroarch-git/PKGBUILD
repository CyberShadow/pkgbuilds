pkgname=retroarch-git
pkgver=1.2.r23.g044ef3d
pkgrel=1

pkgdesc="Simple frontend for the libretro API."
arch=('i686' 'x86_64')
url="http://github.com/libretro/RetroArch"
license=('GPL')

options=('!strip' 'debug')

depends=('mesa' 'sdl' 'nvidia-cg-toolkit')
makedepends=('git')
optdepends=('python: For converting cg shaders to glsl shaders')

provides=('retroarch')
conflicts=('retroarch')

source=('git://github.com/libretro/RetroArch.git')
md5sums=('SKIP')

pkgver() {
    cd RetroArch
    git describe --tags | sed 's/^v//; s/-/.r/; s/-/./'
}

build() {
    cd RetroArch
    ./configure --prefix=/usr \
        --enable-cg \
        --enable-kms \
        --enable-udev \
        --enable-sdl2 \
        --enable-pulse \
        --enable-libxml2 \
        --enable-threads \
        --enable-freetype
    make
}

package() {
    cd RetroArch
    make DESTDIR="$pkgdir" install
}
