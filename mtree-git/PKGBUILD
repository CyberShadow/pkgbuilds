pkgname=mtree-git
pkgver=1.0.3.r7.g373706c
pkgrel=2

pkgdesc='map a directory hierarchy'
url='https://github.com/archiecobbs/mtree-port'
arch=('i686' 'x86_64')
license=('BSD')

depends=('openssl')

source=('git+https://github.com/archiecobbs/mtree-port')

provides=('mtree')
conflicts=('mtree')

sha1sums=('SKIP')

pkgver() {
    cd mtree-port
    git describe --long --tags | sed 's/^v//; s/\([^-]*-g\)/r\1/; s/-/./g'
}

build() {
    cd mtree-port
    ./autogen.sh # XXX This should favour autoreconf instead
    ./configure --prefix=/usr
    make
}

package() {
    cd mtree-port
    install -Dm0755 mtree "$pkgdir"/usr/bin/mtree
    install -Dm0644 mtree.8 "$pkgdir"/usr/share/man/man8/mtree.8

    # XXX libarchive provides an mtree.5 which differs from FreeBSDs mtree format
    install -Dm0644 mtree.5 "$pkgdir"/usr/share/man/man5/mtree-port.5

    install -Dm0644 COPYING "$pkgdir"/usr/share/licenses/"$pkgname"/COPYING
}
