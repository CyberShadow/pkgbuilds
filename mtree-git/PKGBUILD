pkgname=mtree-git
pkgver=1.0.4.r4.ge11b065
pkgrel=1

pkgdesc='map a directory hierarchy'
url='https://github.com/archiecobbs/mtree-port'
arch=('i686' 'x86_64')
license=('BSD')

depends=('openssl')

source=('git+https://github.com/archiecobbs/mtree-port')

provides=('mtree')
conflicts=('mtree')

sha256sums=('SKIP')

pkgver() {
    cd mtree-port
    git describe --long --tags | sed 's/^v//; s/\([^-]*-g\)/r\1/; s/-/./g'
}

build() {
    cd mtree-port
    autoreconf -fi
    ./configure --prefix=/usr
    make
}

package() {
    cd mtree-port
    make DESTDIR="$pkgdir" doc_DATA= install

    # libarchive provides an mtree.5 which differs from BSDs mtree format
    mv "$pkgdir"/usr/share/man/man5/mtree.5 "$pkgdir"/usr/share/man/man5/mtree-port.5

    install -Dm0644 COPYING "$pkgdir"/usr/share/licenses/"$pkgname"/COPYING
}
