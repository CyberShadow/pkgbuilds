pkgname=zathura-djvu-git
pkgver=0.2.5.r4.g6c50123
pkgrel=1

pkgdesc='plugin for djvu support within zathura'
url='https://pwmt.org/projects/zathura/'
arch=('i686' 'x86_64')
license=('zlib')

depends=('girara' 'desktop-file-utils' 'djvulibre')
makedepends=('git' 'wayland-protocols')

provides=('zathura-djvu')
conflicts=('zathura-djvu')

source=('git+https://git.pwmt.org/pwmt/zathura-djvu.git#branch=develop')

sha1sums=('SKIP')

pkgver() {
    cd zathura-djvu
    git describe | sed 's/-/.r/; s/-/./'
}

build() {
    cd zathura-djvu
    make
}

package() {
    cd zathura-djvu
    make DESTDIR="$pkgdir" PREFIX=/usr install
    install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE
}
