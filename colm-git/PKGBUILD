pkgname=colm-git
pkgver=0.13.0.5.r0.g152832c0
pkgrel=2

pkgdesc='programming language designed for the analysis and transformation of computer languages'
url='http://www.colm.net/open-source/colm'
arch=('i686' 'x86_64')
license=('MIT')

depends=('gcc-libs')

source=('git://git.colm.net/colm.git'
        'fix-test-compile.patch')

provides=('colm')
conflicts=('colm')

sha1sums=('SKIP'
          'd0a6989269ed5d35afb09ee5a8aa47b4b7cfd26c')

pkgver() {
    cd colm
    git describe --long --tags | sed 's/^v//; s/\([^-]*-g\)/r\1/; s/-/./g'
}

prepare() {
    cd colm
    # XXX Awaiting moderation(?): http://www.colm.net/pipermail/colm
    patch -Np1 -i "$srcdir"/fix-test-compile.patch
}

build() {
    cd colm
    autoreconf -fi
    ./configure --prefix=/usr
    make
}

check() {
    cd colm/test
    ./autogen.sh
    ./configure
    make
    ./runtests
}

package() {
    cd colm
    make DESTDIR="$pkgdir" install
    install -Dm0644 colm.vim "$pkgdir"/usr/share/vim/vimfiles/syntax/colm.vim
    install -Dm0644 COPYING "$pkgdir"/usr/share/licenses/"$pkgname"/COPYING
}
