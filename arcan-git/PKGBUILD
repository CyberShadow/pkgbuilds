pkgname=arcan-git
pkgver=0.5.3.pre1.r89.g2db25f98
pkgrel=1

pkgdesc='multimedia framework'
url='http://arcan-fe.com/'
arch=('i686' 'x86_64')
license=('GPL' 'BSD')

depends=('ffmpeg' 'openal' 'sqlite3' 'luajit' 'apr' 'libusb')
makedepends=('git' 'cmake')

provides=('arcan')
conflicts=('arcan')

source=('git+https://github.com/letoram/arcan')

sha256sums=('SKIP')

pkgver() {
    cd arcan
    git describe --long --tags | sed -r 's/^v//; s/([^-]*-g)/r\1/; s/-/./g'
}

build() {
    cd arcan
    cmake -DCMAKE_INSTALL_PREFIX=/usr \
          -DCMAKE_BUILD_TYPE=RelWithDebInfo \
          -DVIDEO_PLATFORM=egl-dri src
    make VERBOSE=1
}

package() {
    cd arcan
    make DESTDIR="$pkgdir" install
    install -Dm0644 COPYING "$pkgdir"/usr/share/licenses/"$pkgname"/COPYING

    # Clean up INSTALL_PREFIX assumptions.
    mv "$pkgdir"/usr/man "$pkgdir"/usr/share
    mv "$pkgdir"/usr/CMake "$pkgdir"/usr/lib/cmake
}
