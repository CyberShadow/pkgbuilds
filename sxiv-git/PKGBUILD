pkgname=sxiv-git
pkgver=1.1.1.r18.g3da53fc
pkgrel=1

pkgdesc='A simple X image viewer'
url="https://github.com/Cloudef/sxiv"
arch=('i686' 'x86_64')
license=('GPL2')

depends=('libx11' 'xproto' 'imlib2' 'giflib')
makedepends=('git')

provides=('sxiv')
conflicts=('sxiv')

source=('git://github.com/Cloudef/sxiv#branch=manga'
        'ctrl_x_rm_image.patch'
        'enable_gif_loop.patch')

md5sums=('SKIP'
         '9845557a46b1c8dc98751f806bc256f3'
         'df4344d52f87ac1dd6624794aa07c1fd')

pkgver() {
    cd sxiv
    git describe | sed 's/^v//; s/-/.r/; s/-/./'
}

prepare() {
    cd sxiv
    patch -Np1 -i "$srcdir"/enable_gif_loop.patch
    patch -Np1 -i "$srcdir"/ctrl_x_rm_image.patch
}

build() {
    cd sxiv
    make
}

package() {
    cd sxiv
    make PREFIX=/usr DESTDIR="$pkgdir" install
}
