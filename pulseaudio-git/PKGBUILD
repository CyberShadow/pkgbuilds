pkgname=pulseaudio-git
pkgver=7.0.r53.gbea3761
pkgrel=1

pkgdesc='A network transparent sound server'
url='http://pulseaudio.org/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL')

depends=('rtkit' 'libltdl' 'speex' 'tdb' 'fftw' 'orc' 'libsamplerate' 'python'
         'webrtc-audio-processing' 'sbc' 'libasyncns' 'libxtst' 'libsm' 'json-c'
         'pulseaudio-alsa')
makedepends=('git' 'attr' 'intltool' 'jack' 'lirc-utils' 'openssl')
optdepends=('alsa-plugins: ALSA support.'
            'jack: jack support.'
            'lirc-utils: infra-red support.'
            'openssl: RAOP support.'
            'python2-pyqt: Equalizer GUI (qpaeq).')

provides=('pulseaudio' 'libpulse')
conflicts=('pulseaudio' 'libpulse')

source=('git+https://github.com/pulseaudio/pulseaudio.git')

sha256sums=('SKIP')

pkgver() {
    cd pulseaudio
    git describe | sed 's/^v//; s/-/.r/; s/-/./'
}

prepare() {
    cd pulseaudio
    sed -i 's/^; flat-volumes = yes/flat-volumes = no/' src/daemon/daemon.conf.in
    sed -i 's/^; autospawn = yes/autospawn = no/' src/pulse/client.conf.in
}

build() {
    # XXX Cannot use LTO https://gcc.gnu.org/bugzilla/show_bug.cgi?id=65982
    CFLAGS=${CFLAGS/-flto/}
    CXXFLAGS=${CXXFLAGS/-flto/}

    # Make sure to set the bash completion directory here as without it the
    # makefile will naively expand $(bashcompletiondir) to nothing and create
    # symlinks directly under $(DESTDIR).
    cd pulseaudio
    ./autogen.sh --prefix=/usr \
        --enable-orc \
        --disable-xen \
        --disable-dbus \
        --disable-gconf \
        --disable-avahi \
        --disable-rpath \
        --disable-bluez4 \
        --disable-esound \
        --disable-tcpwrap \
        --sysconfdir=/etc \
        --disable-hal-compat \
        --with-database=gdbm \
        --localstatedir=/var \
        --libexecdir=/usr/lib \
        --disable-systemd-daemon \
        --disable-default-build-tests \
        --disable-per-user-esound-socket \
        --disable-legacy-database-entry-format \
        --with-udev-rules-dir=/usr/lib/udev/rules.d \
        --with-bash-completion-dir=/usr/share/bash-completion/completions
    make
}

package() {
    cd pulseaudio
    make  DESTDIR="$pkgdir" install
    rm -rf "$pkgdir"/etc/dbus-1
    rm -rf "$pkgdir"/etc/xdg
}
