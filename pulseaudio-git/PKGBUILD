pkgname=pulseaudio-git
pkgver=8.0.r102.gfcee3da
pkgrel=1

pkgdesc='network transparent sound server'
url='http://pulseaudio.org/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL')

depends=('rtkit' 'libltdl' 'speex' 'tdb' 'fftw' 'orc' 'libsamplerate' 'python'
         'webrtc-audio-processing' 'sbc' 'libasyncns' 'libxtst' 'libsm' 'json-c'
         'pulseaudio-alsa')
makedepends=('git' 'attr' 'intltool' 'jack' 'lirc-utils' 'openssl')
optdepends=('alsa-plugins: alsa support'
            'jack: jack support'
            'lirc-utils: infra-red support'
            'openssl: raop support'
            'python2-pyqt: equalizer gui (qpaeq)')

provides=('pulseaudio' 'libpulse')
conflicts=('pulseaudio' 'libpulse')

source=('git+https://github.com/pulseaudio/pulseaudio.git')

sha1sums=('SKIP')

pkgver() {
    cd pulseaudio
    git describe | sed 's/^v//; s/-/.r/; s/-/./'
}

prepare() {
    cd pulseaudio
    sed -i 's/^; flat-volumes = yes/flat-volumes = no/' src/daemon/daemon.conf.in
    sed -i 's/^; autospawn = yes/autospawn = no/' src/pulse/client.conf.in

    # Enabled memfd instead of POSIX shm.
    printf 'enable-memfd = yes\n' >> src/daemon/daemon.conf.in
}

build() {
    # Make sure to set the bash completion directory here as without it the
    # makefile will naively expand $(bashcompletiondir) to nothing and create
    # symlinks directly under $(DESTDIR).
    cd pulseaudio
    # XXX requires -fPIC to build with -flto
    CFLAGS=-fPIC ./autogen.sh \
        --prefix=/usr \
        --enable-orc \
        --disable-x11 \
        --disable-xen \
        --disable-dbus \
        --disable-gtk3 \
        --disable-gconf \
        --disable-avahi \
        --disable-glib2 \
        --disable-rpath \
        --disable-bluez4 \
        --disable-esound \
        --disable-tcpwrap \
        --sysconfdir=/etc \
        --disable-hal-compat \
        --with-database=gdbm \
        --localstatedir=/var \
        --libexecdir=/usr/lib \
        --disable-systemd-daemon \
        --disable-default-build-tests \
        --disable-per-user-esound-socket \
        --disable-legacy-database-entry-format \
        --with-udev-rules-dir=/usr/lib/udev/rules.d \
        --with-bash-completion-dir=/usr/share/bash-completion/completions
    make
}

package() {
    cd pulseaudio
    make  DESTDIR="$pkgdir" install
    rm -rf "$pkgdir"/etc/dbus-1
    rm -rf "$pkgdir"/etc/xdg
}
