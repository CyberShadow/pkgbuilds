# Maintainer: Earnestly <zibeon@gmail.com>

pkgname=antimicro-git
pkgver=2.1.r30.g304af7f
pkgrel=1

pkgdesc='Map keyboard and mouse actions to gamepad buttons, inspired by qjoypad.'
url='https://github.com/Ryochan7/antimicro'
arch=('i686' 'x86_64')
license=('GPL3')

depends=('glibc' 'libxtst' 'qt5-base' 'sdl2')
makedepends=('git' 'qt5-tools')

provides=('antimicro')
conflicts=('antimicro')

install='antimicro-git.install'

source=('git://github.com/Ryochan7/antimicro')

md5sums=('SKIP')

pkgver() {
    cd antimicro
    git describe | sed 's/-/.r/; s/-/./'
}

build() {
    cd antimicro/src
    qmake-qt5 INSTALL_PREFIX=/usr USE_SDL_2=1
    make
}

package() {
    cd antimicro/src
    make INSTALL_ROOT="$pkgdir" install
}
