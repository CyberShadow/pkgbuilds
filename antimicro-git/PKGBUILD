pkgname=antimicro-git
pkgver=2.6.r5.gcc02e95
pkgrel=1

pkgdesc='Map keyboard and mouse actions to gamepad buttons, inspired by qjoypad.'
url='https://github.com/Ryochan7/antimicro'
arch=('i686' 'x86_64')
license=('GPL3')

depends=('libxtst' 'qt5-base' 'sdl2' 'libxkbcommon-x11')
makedepends=('git' 'cmake' 'qt5-tools')

provides=('antimicro')
conflicts=('antimicro')

install='antimicro-git.install'

source=('git://github.com/Ryochan7/antimicro')

md5sums=('SKIP')

pkgver() {
    cd antimicro
    git describe | sed 's/-/.r/; s/-/./'
}

build() {
    cd antimicro
    cmake -DCMAKE_INSTALL_PREFIX=/usr \
          -DUSE_SDL_2=ON \
          -DWITH_XTEST=OFF \
          -DWITH_UINPUT=ON
    make
}

package() {
    cd antimicro
    make DESTDIR="$pkgdir" install
}
