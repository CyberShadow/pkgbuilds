# XXX An upset has resulted in main repo taken down. Using a mirror instead
#     https://github.com/Ryochan7/antimicro-packaging/issues/1
pkgname=antimicro-git
pkgver=2.21.r19.gf3764a8
pkgrel=1

pkgdesc='map keyboard and mouse actions to gamepad buttons, inspired by qjoypad'
url='https://github.com/7185/antimicro'
#url='https://github.com/Ryochan7/antimicro'
arch=('i686' 'x86_64')
license=('GPL3')

depends=('libxtst' 'qt5-base' 'sdl2' 'libxkbcommon-x11')
makedepends=('cmake' 'qt5-tools')

provides=('antimicro')
conflicts=('antimicro')

source=('git+https://github.com/7185/antimicro')
#source=('git+https://github.com/Ryochan7/antimicro')

install='antimicro-git.install'

sha1sums=('SKIP')

pkgver() {
    cd antimicro
    git describe | sed 's/-/.r/; s/-/./'
}

build() {
    cd antimicro
    # XXX Requires -fPIC to use -flto https://github.com/Ryochan7/antimicro/issues/263
    CXXFLAGS=-fPIC cmake -DCMAKE_INSTALL_PREFIX=/usr -DUSE_SDL_2=ON
    make
}

package() {
    cd antimicro
    make DESTDIR="$pkgdir" install
}
