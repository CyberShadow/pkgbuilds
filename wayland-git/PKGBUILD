pkgname=wayland-git
pkgver=1.12.0.r40.g721e0b4
pkgrel=1

pkgdesc='display server protocol'
url='http://wayland.freedesktop.org'
arch=('i686' 'x86_64')
license=('MIT')

depends=('libffi' 'expat' 'libxml2')
makedepends=('doxygen' 'xmlto' 'docbook-xsl' 'graphviz')

provides=('wayland')
conflicts=('wayland')

source=('git+https://anongit.freedesktop.org/git/wayland/wayland')

sha1sums=('SKIP')

pkgver() {
    cd wayland
    git describe | sed 's/-/.r/; s/-/./'
}

build() {
    cd wayland
    ./autogen.sh --prefix=/usr --disable-static
    make
}

package() {
    cd wayland
    make DESTDIR="$pkgdir" install
    install -Dm644 COPYING "$pkgdir"/usr/share/licenses/"$pkgname"/COPYING
}
