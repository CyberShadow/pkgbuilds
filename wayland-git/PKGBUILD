pkgname=wayland-git
pkgver=1.7.0.r8.g439b0a3
pkgrel=2

pkgdesc='A computer display server protocol'
url='http://wayland.freedesktop.org'
arch=('i686' 'x86_64')
license=('MIT')

depends=('libffi' 'expat')
makedepends=('doxygen' 'xmlto' 'graphviz')

provides=('wayland')
conflicts=('wayland')

source=('git://anongit.freedesktop.org/wayland/wayland')

md5sums=('SKIP')

options=('debug' '!strip')

pkgver() {
    cd wayland
    git describe | sed 's/-/.r/; s/-/./'
}

build() {
    cd wayland
    ./autogen.sh
    ./configure --prefix=/usr --disable-static
    make
}

package() {
    cd wayland
    make DESTDIR="$pkgdir" install
    install -Dm644 COPYING "$pkgdir"/usr/share/licenses/"$pkgname"/COPYING
}
