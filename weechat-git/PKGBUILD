pkgname=weechat-git
pkgver=1.9.r447.g1f9b83aac
pkgrel=1

pkgdesc='terminal based irc client'
url='http://www.weechat.org/'
arch=('i686' 'x86_64')
license=('GPL3')

depends=('curl' 'gnutls' 'libgcrypt' 'hicolor-icon-theme')
makedepends=('git' 'asciidoc' 'source-highlight' 'aspell' 'cmake' 'lua' 'perl'
             'python' 'ruby' 'tcl' 'guile' 'lua' 'php-embed')
checkdepends=('cpputest')
optdepends=('aspell' 'lua' 'perl' 'python' 'ruby' 'tcl' 'guile' 'php-embed')

provides=('weechat')
conflicts=('weechat')

source=('git+https://github.com/weechat/weechat.git')

sha256sums=('SKIP')

pkgver() {
    cd weechat
    git describe --long --tags | sed 's/^v//; s/\([^-]*-g\)/r\1/; s/-/./g'
}

build() {
    cd weechat
    _python=$(python -c 'import sys; print("{}.{}m".format(*sys.version_info))')

    cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DENABLE_JAVASCRIPT=OFF \
        -DENABLE_PYTHON3=ON \
        -DPYTHON_LIBRARY=/usr/lib/libpython"$_python".so \
        -DENABLE_TESTS=ON
    make VERBOSE=1
}

# XXX These tests require the en_US.UTF-8 locale and charmap
check() {
    cd weechat

    # XXX Currently there's no way to disable testing of specific plugins which
    #     we can't support.  Because of this some tests will fail.
    ctest -V || true
}

package() {
    cd weechat
    make DESTDIR="$pkgdir" install
}
