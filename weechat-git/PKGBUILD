# Maintainer: Earnestly <zibeon@gmail.com>

pkgname=weechat-git
pkgver=0.4.3200.rgbd06f0f
pkgrel=1

pkgdesc="Fast, light & extensible IRC client (ncurses UI)"
arch=('i686' 'x86_64')
url="http://www.weechat.org/"
license=('GPL3')

depends=('ncurses-git' 'gnutls')
makedepends=('git' 'aspell' 'cmake' 'lua' 'perl' 'python2' 'ruby' 'tcl')
optdepends=('aspell' 'lua' 'perl' 'python2' 'ruby' 'tcl')

provides=('weechat')
conflicts=('weechat')

source=(git://git.sv.gnu.org/weechat.git)
md5sums=('SKIP')

pkgver() {
    cd weechat 
    git describe | sed 's/^v//; s/-//; s/-/.r/; s/-/./'
}

build() {
    cd weechat

    _xdgconf="${XDG_CONFIG_HOME:-$HOME/.config}"

    cmake -DPREFIX=/usr -DWEECHAT_HOME="$_xdgconf"/weechat \
        -DPYTHON_EXECUTABLE=/usr/bin/python2 \
        -DPYTHON_LIBRARY=/usr/lib/libpython2.7.so
}

package() {
    cd weechat
    make DESTDIR="$pkgdir" install
}

