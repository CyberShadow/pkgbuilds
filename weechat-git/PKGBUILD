pkgname=weechat-git
pkgver=1.9.r270.g387ddb98a
pkgrel=1

pkgdesc='terminal based irc client'
url='http://www.weechat.org/'
arch=('i686' 'x86_64')
license=('GPL3')

depends=('curl' 'gnutls' 'libgcrypt' 'hicolor-icon-theme')
makedepends=('git' 'asciidoc' 'source-highlight' 'aspell' 'cmake' 'lua' 'perl'
             'python' 'ruby' 'tcl' 'guile' 'lua')
checkdepends=('cpputest')
optdepends=('aspell' 'lua' 'perl' 'python' 'ruby' 'tcl' 'guile')

provides=('weechat')
conflicts=('weechat')

source=('git+https://github.com/weechat/weechat.git')

sha1sums=('SKIP')

pkgver() {
    cd weechat
    git describe --long --tags | sed 's/^v//; s/\([^-]*-g\)/r\1/; s/-/./g'
}

build() {
    cd weechat
    _python=$(python -c 'import sys; print("{}.{}m".format(*sys.version_info))')

    cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DPYTHON_LIBRARY=/usr/lib/libpython"$_python".so \
        -DENABLE_PYTHON3=ON \
        -DENABLE_TESTS=ON
    make
}

# XXX These tests require the en_US.UTF-8 locale and charmap
check() {
    cd weechat
    ctest -V
}

package() {
    cd weechat
    make DESTDIR="$pkgdir" install
}
