pkgname=polyml-git
pkgver=5.7.1.r7.g01482bfb
pkgrel=1

pkgdesc='standard ml implementation'
url='http://www.polyml.org/'
arch=('i686' 'x86_64')
license=('GPL')

options=('staticlibs' '!libtool')

depends=('libffi' 'gcc-libs')
makedepends=('git')

provides=('polyml')

# XXX mesa-demos has claimed /bin/poly
conflicts=('polyml' 'mesa-demos')

source=('git+https://github.com/polyml/polyml')

sha256sums=('SKIP')

pkgver() {
    cd polyml
    git describe --long --tags | sed 's/^v//; s/-/.r/; s/-/./'
}

build() {
    cd polyml
    ./configure --prefix=/usr --with-system-libffi --with-gmp
    make
    make compiler
}

check() {
    cd polyml
    make tests
}

package() {
    cd polyml
    make DESTDIR="$pkgdir" install
}
