pkgname=llpp-git
pkgver=21.r63.g6686874
pkgrel=1

pkgdesc='A lightweight, opengl accelerated PDF viewer based on MuPDF.'
arch=('i686' 'x86_64')
url='http://repo.or.cz/w/llpp.git'
license=('GPL')

depends=('glu' 'libgl' 'openjpeg2' 'jbig2dec' 'libjpeg-turbo' 'freetype2')
makedepends=('git' 'ocaml' 'ocaml-lablgl' 'mupdf' 'camlp4')
optdepends=('djvulibre: llppac djvu conversion'
            'ghostscript: llppac ps, dvi, and djvu conversion'
            'princexml: llppac html conversion'
            'unoconv: llppac office conversion'
            'librsvg: llppac svg conversion'
            'imagemagick: llppac image conversion')

provides=('llpp')
conflicts=('llpp')

source=('git://repo.or.cz/llpp.git'
        'fix_xdg_usage.patch'
        'no_quit_on_escape.patch'
        'Makefile')

md5sums=('SKIP'
         'ddfe199327a74d2b363df564642cde81'
         '07fcbe68a2ccce6876939c589b9ea363'
         'a78b0605f2b5285c16836b3c3c68eb2e')

pkgver() {
    cd llpp
    git describe --tags | sed 's/^v//; s/-/.r/; s/-/./'
}

prepare() {
    cd llpp
    patch -Np1 -i "$srcdir"/fix_xdg_usage.patch
    patch -Np1 -i "$srcdir"/no_quit_on_escape.patch

    # 2015-02-28 Revert random API breakage due to adhoc "sync with upstream"
    # mupdf.  Sigh.
    #git revert --no-edit b1a63f94092673a0d75399a108a1c706fa8dbc21
}

build() {
    cd llpp
    make -f "$srcdir"/Makefile
}

package() {
    cd llpp
    make -f "$srcdir"/Makefile PREFIX=/usr DESTDIR="$pkgdir" install

    cd misc/completions
    make  PREFIX=/usr DESTDIR="$pkgdir" install
}
