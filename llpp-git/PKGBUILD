pkgname=llpp-git
pkgver=20.r10.g6b3a3ed
pkgrel=2

pkgdesc='A lightweight, opengl accelerated PDF viewer based on MuPDF.'
arch=('i686' 'x86_64')
url='http://repo.or.cz/w/llpp.git'
license=('GPL')

depends=('glibc' 'ncurses' 'glu' 'libgl' 'libx11' 'openjpeg2' 'jbig2dec' 'libjpeg-turbo' 'freetype2')
makedepends=('git' 'ocaml' 'ocaml-lablgl' 'mupdf')
optdepends=('djvulibre: llppac djvu conversion'
            'ghostscript: llppac ps, dvi, and djvu conversion'
            'princexml: llppac html conversion'
            'unoconv: llppac office conversion'
            'librsvg: llppac svg conversion'
            'imagemagick: llppac image conversion')

provides=('llpp')
conflicts=('llpp')

source=('git://repo.or.cz/llpp.git' 
        'Makefile'
        'use_xdg_config.patch'
        'llpp.desktop')

md5sums=('SKIP'
         '11752bcc61dfa6014464c72c48063a29'
         '3cefe08fa4e074adf7827f3159053084'
         '9c4891f779eac44569671ee3a9cc0335')

pkgver() {
    cd llpp
    git describe --tags | sed 's/^v//; s/-/.r/; s/-/./'
}

prepare() {
    cd llpp
    patch -Np1 -i "$srcdir"/use_xdg_config.patch
}

build() {
    cd llpp
    make -f "$srcdir"/Makefile
}

package() {
    cd llpp
    make -f "$srcdir"/Makefile PREFIX=/usr DESTDIR="$pkgdir" install
    install -Dm644 "$srcdir"/llpp.desktop "$pkgdir"/usr/share/applications/llpp.desktop
}
