pkgname=llpp-git
pkgver=26b.r52.g9bd13e9
pkgrel=1

pkgdesc='opengl accelerated pdf viewer based on mupdf'
url='http://repo.or.cz/w/llpp.git'
arch=('i686' 'x86_64')
license=('GPL')

depends=('glu' 'openjpeg2' 'jbig2dec' 'libjpeg-turbo' 'freetype2' 'desktop-file-utils')
makedepends=('git' 'ocaml' 'ocaml-lablgl' 'libmupdf' 'camlp4')
optdepends=('djvulibre: llppac djvu conversion'
            'ghostscript: llppac ps, dvi, and djvu conversion'
            'princexml: llppac html conversion'
            'unoconv: llppac office conversion'
            'librsvg: llppac svg conversion'
            'imagemagick: llppac image conversion'
            'llpp-gc: clean nonextant history items')

provides=('llpp')
conflicts=('llpp')

# llpp seems to go through build systems like fasion, holomorph has provided a
# very nice Makefile for building this software which has served well for
# months.  It was contributed, or offered, to the author malc, but he concluded
# that make was bad and needed constant effort to prevent "bitrot" as
# justification for using a terrible shell script that downloaded arbitrary
# commits of bundled libraries.  Now he's on to shake, a haskell NIH
# implementation of make requiring that you now also have ghc and the shake
# library to build this software.  Before this he used ninja.

# For obvious reasons I'm sticking with the custom makefile which greatly
# reduces maintainence burden and unecessary dependency bloat.
source=('git://repo.or.cz/llpp.git'
        'no-quit-on-escape.patch'
        'fix-xdg-usage.patch'
        'Makefile')

sha1sums=('SKIP'
          '3329c58376aca840cf6a8b42410492791fba92ff'
          'e380e593c52b1bb991f15e52f92f38c87942c652'
          '1031dc7904105f11eed770b14566e8ce2bde2fbb')

pkgver() {
    cd llpp
    git describe --tags | sed 's/^v//; s/-/.r/; s/-/./'
}

prepare() {
    cd llpp
    patch -Np1 -i "$srcdir"/fix-xdg-usage.patch
    patch -Np1 -i "$srcdir"/no-quit-on-escape.patch
}

build() {
    cd llpp
    make -f "$srcdir"/Makefile
}

package() {
    cd llpp
    make -f "$srcdir"/Makefile PREFIX=/usr DESTDIR="$pkgdir" install

    cd misc/completions
    make PREFIX=/usr DESTDIR="$pkgdir" install
}
