pkgname=llpp-git
pkgver=18
pkgrel=1

pkgdesc='A lightweight, opengl accelerated PDF viewer based on MuPDF.'
arch=('i686' 'x86_64')
url='http://repo.or.cz/w/llpp.git'
license=('GPL')

depends=('glibc' 'ncurses' 'glu' 'libgl' 'libx11' 'openjpeg2' 'jbig2dec' 'libjpeg-turbo' 'freetype2')
makedepends=('git' 'ocaml' 'ocaml-lablgl' 'mupdf' 'mujs-git')

provides=('llpp')
conflicts=('llpp')

source=('git://repo.or.cz/llpp.git' 
        'Makefile'
        'use_xdg_env.patch'
        'llpp.desktop')

md5sums=('SKIP'
         'ef2404ad1f9253793cb5b7739789f4e4'
         'f50b5aa11e001d89b1d296791ffa84d4'
         '9c4891f779eac44569671ee3a9cc0335')

pkgver() {
    cd llpp
    git describe --tags | sed 's/^v//; s/-/.r/; s/-/./'
}

prepare() {
    cd llpp
    patch -Np1 -i "$srcdir"/use_xdg_env.patch
}

build() {
    cd llpp
    make -f "$srcdir"/Makefile PREFIX=/usr
}

package() {
    cd llpp
    make -f "$srcdir"/Makefile PREFIX=/usr DESTDIR=$pkgdir install
    install -Dm644 "$srcdir"/llpp.desktop "$pkgdir"/usr/share/applications/llpp.desktop
}
