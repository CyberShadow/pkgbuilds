pkgname=llpp-git
pkgver=22.r10.gda3288a
pkgrel=1

pkgdesc='A lightweight, opengl accelerated PDF viewer based on MuPDF.'
arch=('i686' 'x86_64')
url='http://repo.or.cz/w/llpp.git'
license=('GPL')

depends=('glu' 'openjpeg2' 'jbig2dec' 'libjpeg-turbo' 'freetype2' 'desktop-file-utils')
makedepends=('git' 'ocaml' 'ocaml-lablgl' 'mupdf' 'camlp4')
optdepends=('djvulibre: llppac djvu conversion'
            'ghostscript: llppac ps, dvi, and djvu conversion'
            'princexml: llppac html conversion'
            'unoconv: llppac office conversion'
            'librsvg: llppac svg conversion'
            'imagemagick: llppac image conversion')

provides=('llpp')
conflicts=('llpp')

source=('git://repo.or.cz/llpp.git'
        'fix_xdg_usage.patch'
        'no_quit_on_escape.patch'
        'Makefile')

sha1sums=('SKIP'
          'e380e593c52b1bb991f15e52f92f38c87942c652'
          '2c0a383c6540e0b4eb965b0f138c5b6dceb4f757'
          'd94bcdded6ab3ebcfdecabe42fbbef059c528cd5')

install='llpp-git.install'

pkgver() {
    cd llpp
    git describe --tags | sed 's/^v//; s/-/.r/; s/-/./'
}

prepare() {
    cd llpp
    patch -Np1 -i "$srcdir"/fix_xdg_usage.patch
    patch -Np1 -i "$srcdir"/no_quit_on_escape.patch
}

build() {
    cd llpp
    make -f "$srcdir"/Makefile
}

package() {
    cd llpp
    make -f "$srcdir"/Makefile PREFIX=/usr DESTDIR="$pkgdir" install

    cd misc/completions
    make  PREFIX=/usr DESTDIR="$pkgdir" install
}
