pkgname=llpp-git
pkgver=20.r50.gbb1ce65
pkgrel=1

pkgdesc='A lightweight, opengl accelerated PDF viewer based on MuPDF.'
arch=('i686' 'x86_64')
url='http://repo.or.cz/w/llpp.git'
license=('GPL')

depends=('glibc' 'ncurses' 'glu' 'libgl' 'libx11' 'openjpeg2' 'jbig2dec' 'libjpeg-turbo' 'freetype2')
makedepends=('git' 'ocaml' 'ocaml-lablgl' 'mupdf')
optdepends=('djvulibre: llppac djvu conversion'
            'ghostscript: llppac ps, dvi, and djvu conversion'
            'princexml: llppac html conversion'
            'unoconv: llppac office conversion'
            'librsvg: llppac svg conversion'
            'imagemagick: llppac image conversion')

provides=('llpp')
conflicts=('llpp')

source=('git://repo.or.cz/llpp.git' 
        'fix_xdg_usage.patch'
        'Makefile')

md5sums=('SKIP'
         '1ace0d5a0b4d6b9a788ffc98f4e5a74c'
         '8c419b0d2b515f437aad6c225c270f7b')

pkgver() {
    cd llpp
    git describe --tags | sed 's/^v//; s/-/.r/; s/-/./'
}

prepare() {
    cd llpp
    patch -Np1 -i "$srcdir"/fix_xdg_usage.patch
}

build() {
    cd llpp
    make -f "$srcdir"/Makefile
}

package() {
    cd llpp
    make -f "$srcdir"/Makefile PREFIX=/usr DESTDIR="$pkgdir" install

    cd misc/completions
    make  PREFIX=/usr DESTDIR="$pkgdir" install
}
