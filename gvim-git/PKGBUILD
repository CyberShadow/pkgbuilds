pkgname=gvim-git
pkgver=7.4.826.b60dc1b
pkgrel=1

pkgdesc='Vi Improved, a highly configurable, improved version of the vi text editor.'
url='http://www.vim.org/'
arch=('i686' 'x86_64')
license=('custom:vim')

groups=('editors')

depends=('gtk2' 'lua' 'python' 'ruby' 'desktop-file-utils' 'gawk' 'libxt')
makedepends=('mercurial')

conflicts=('vim' 'gvim' 'vim-runtime')
provides=('vim' 'gvim' 'vim-runtime')

source=('git://github.com/vim/vim'
        'gvim.desktop'
        'remove_ro_delay.patch')

install='gvim-git.install'

md5sums=('SKIP'
         '6e11c556ba3f2ce7dc05d9908188d604'
         '0e2d8ef1e98a58614d441b24f2242b98')

pkgver() {
    cd vim
    # Vim's weird patch based versioning system, yay.
    printf '%s.%s' $(git describe --tags | sed 's/^v//; s/-/./g;') $(git rev-parse --short HEAD)
}

prepare() {
    cd vim
    patch -Np1 -i "$srcdir"/remove_ro_delay.patch
}

build() {
    cd vim
    ./configure \
        --enable-acl \
        --disable-gpm \
        --with-x=yes \
        --prefix=/usr \
        --enable-cscope \
        --enable-gui=gtk2 \
        --enable-luainterp \
        --enable-multibyte \
        --disable-netbeans \
        --enable-perlinterp \
        --enable-rubyinterp \
        --with-features=huge \
        --disable-pythoninterp \
        --enable-python3interp \
        --localstatedir=/var/lib/vim
    make
}

package() {
    cd vim
    make DESTDIR="$pkgdir" install

    rm "$pkgdir"/usr/bin/ex
    rm "$pkgdir"/usr/bin/view

    shopt -s globstar
    rm "$pkgdir"/usr/share/man/**/ex.1*
    rm "$pkgdir"/usr/share/man/**/view.1*
    shopt -u globstar

    install -Dm644 "$srcdir"/gvim.desktop "$pkgdir"/usr/share/applications/gvim.desktop
    install -Dm644 runtime/vim48x48.png "$pkgdir"/usr/share/pixmaps/gvim.png
    install -Dm644 runtime/doc/uganda.txt "$pkgdir"/usr/share/licenses/"$pkgname"/license.txt
}
