pkgname=dolphin-emu-git
pkgver=4.0.r9318.gec40b38
pkgrel=1

pkgdesc='gamecube, wii and triforce emulator'
url='http://www.dolphin-emu.org/'
arch=('i686' 'x86_64')
license=('GPL2')

depends=('bluez-libs' 'enet' 'ffmpeg' 'libao' 'libevdev' 'mbedtls' 'miniupnpc'
         'sfml' 'soundtouch' 'libpng' 'soil' 'libusb' 'lzo' 'hicolor-icon-theme')
makedepends=('cmake' 'git' 'opencl-headers')

provides=('dolphin-emu')
conflicts=('dolphin-emu')

source=('git+https://github.com/dolphin-emu/dolphin')

sha1sums=('SKIP')

pkgver() {
    cd dolphin
    git describe | sed 's/-/.r/; s/-/./'
}

build() {
    cd dolphin
    cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DENABLE_QT2=OFF \
        -DENABLE_PCH=OFF \
        -DDISABLE_WX=ON \
        -DENABLE_LTO=ON
    make
}

package() {
    cd dolphin
    make DESTDIR="$pkgdir" install
}
