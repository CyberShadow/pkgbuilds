pkgname=dolphin-emu-git
pkgver=4.0.r7668.gc319fbb
pkgrel=1

pkgdesc='A GameCube / Wii / Triforce emulator'
url='http://www.dolphin-emu.org/'
arch=('i686' 'x86_64')
license=('GPL2')

depends=('bluez-libs' 'ffmpeg' 'glew' 'libao' 'libusbx' 'miniupnpc' 'openal' 
         'portaudio' 'sdl2' 'soundtouch' 'wxgtk')
makedepends=('cmake' 'git' 'opencl-headers')
optdepends=('pulseaudio: PulseAudio backend')

provides=('dolphin-emu')
conflicts=('dolphin-emu')

source=('git+https://github.com/dolphin-emu/dolphin')

sha1sums=('SKIP')

pkgver() {
    cd dolphin
    git describe | sed 's/-/.r/; s/-/./'
}

build() {
    cd dolphin
    cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=RelWithDebInfo
    make
}

package() {
    cd dolphin
    make DESTDIR="$pkgdir" install
}
