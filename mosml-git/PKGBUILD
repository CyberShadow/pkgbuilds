pkgname=mosml-git
pkgver=2.10.1.r54.gf529b33
pkgrel=1

pkgdesc='the moscow standard ml implementation'
url='http://www.itu.dk/~sestoft/mosml.html'
arch=('i686' 'x86_64')
license=('GPL')

depends=('glibc')
makedepends=('git')

provides=('mosml')
conflicts=('mosml')

source=('git+https://github.com/kfl/mosml')

sha1sums=('SKIP')

pkgver() {
    cd mosml
    git describe --long --tags | sed 's/^ver-//; s/-/.r/; s/-/./'
}

build() {
    cd mosml/src
    make BINDIR=/usr/bin LIBDIR=/usr/lib/mosml world
}

package() {
    cd mosml/src
    make PREFIX=/usr DESTDIR="$pkgdir" install

    # This isn't done by the Makefile.
    for man in "$srcdir"/mosml/man/*.1; do
        install -Dm0644 "$man" "$pkgdir"/usr/share/man/man1/"${man##*/}"
    done
}
