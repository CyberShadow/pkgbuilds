# Maintainer: Earnestly <zibeon@gmail.com>

pkgname=mosml-git
pkgver=2.10.0.c25e10e
pkgrel=1
epoch=1

pkgdesc='Moscow ML is a light-weight implementation of Standard ML (SML)'
url="http://www.itu.dk/~sestoft/mosml.html"
license=('GPL')
arch=('i686' 'x86_64')

depends=('glibc')
makedepends=('git')

provides=('mosml')
conflicts=('mosml')

source=('git+https://github.com/kfl/mosml')
md5sums=('SKIP')

pkgver() {
    cd mosml

    # Hopefully a temporary hack, just append the last commit hash to the tag
    tag=$(git describe --tags | sed -e 's/^ver-//')
    printf '%s.%s' "$tag" "$(git rev-parse --short HEAD)"
}

build() {
    cd mosml/src
    make BINDIR=/usr/bin LIBDIR=/usr/lib/mosml world
}

package() {
    cd mosml/src
    make PREFIX=/usr DESTDIR="$pkgdir" install

    # This isn't done by the Makefile
    cd ../man
    for manual in *.1; do
        install -Dm644 "$manual" "$pkgdir"/usr/share/man/man1/"$manual"
    done
}
