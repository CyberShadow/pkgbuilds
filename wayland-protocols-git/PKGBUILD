pkgname=wayland-protocols-git
pkgver=r86.f93680e
pkgrel=1

pkgdesc='additional protocols not available in wayland core'
url='http://wayland.freedesktop.org'
arch=('any')
license=('MIT')

provides=('wayland-protocols')
conflicts=('wayland-protocols')

source=('git+https://anongit.freedesktop.org/git/wayland/wayland-protocols')

sha1sums=('SKIP')

pkgver() {
    cd wayland-protocols
    printf 'r%s.%s' $(git rev-list --count HEAD) $(git rev-parse --short HEAD)
}

build() {
    cd wayland-protocols
    ./autogen.sh --prefix=/usr
    make
}

package() {
    cd wayland-protocols
    make DESTDIR="$pkgdir" install
    install -Dm644 COPYING "$pkgdir"/usr/share/licenses/"$pkgname"/COPYING
}
