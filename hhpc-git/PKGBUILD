pkgname=hhpc-git
pkgver=0.3.1.r5.g44e8473
pkgrel=1

pkgdesc='A small utility to hide the mouse pointer with X.org'
url='https://github.com/Aktau/hhpc'
arch=('i686' 'x86_64')
license=('BSD')

depends=('libx11')
makedepends=('git')

provides=('hhpc')
conflicts=('hhpc')

source=('git+https://github.com/Aktau/hhpc.git')

sha1sums=('SKIP')

pkgver() {
    cd hhpc
    git describe | sed 's/^v//; s/-/.r/; s/-/./'
}

prepare() {
    cd hhpc
    # Extract the LICENSE.
    sed -n '/^Copyright/,$p' README.md > LICENSE
}

build() {
    cd hhpc
    make
}

package() {
    cd hhpc
    install -Dm755 hhpc "$pkgdir"/usr/bin/hhpc
    install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE
}
