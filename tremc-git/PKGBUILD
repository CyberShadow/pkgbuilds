pkgname=tremc-git
pkgver=r663.g9755b50
pkgrel=1

pkgdesc='curses frontend to transmission-daemon'
url='https://github.com/louipc/tremc'
arch=('any')
license=('GPL')

depends=('python')
makedepends=('git')
optdepends=('python-geoip: guess peer country')

provides=('tremc')
conflicts=('tremc')

source=('git+https://github.com/louipc/tremc')

sha1sums=('SKIP')

pkgver() {
    cd tremc
    printf 'r%s.g%s' "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
    cd tremc
    install -Dm0755 tremc "$pkgdir"/usr/bin/tremc
    install -Dm0644 tremc.1 "$pkgdir"/usr/share/man/man1/tremc.1

    # Still referencing the transmission-remote-cli project
    # install -Dm0644 completion/bash/transmission-remote-cli-bash-completion.sh "$pkgdir"/usr/share/bash-completion/completions/transmission-remote-cli
    # install -Dm0644 completion/zsh/_transmission-remote-cli "$pkgdir"/usr/share/zsh/site-functions/_transmission-remote-cli
}
