pkgname=newsbeuter-git
pkgver=2.9.r545.gd1c116d
pkgrel=1

pkgdesc='rss feed reader for the text console with podcast support'
url='http://www.newsbeuter.org'
arch=('i686' 'x86_64')
license=('MIT')

depends=('sqlite3' 'curl' 'libxml2' 'stfl' 'json-c')
makedepends=('git' 'swig' 'asciidoc' 'docbook-xsl' 'pkgconfig')

provides=('newsbeuter')
conflicts=('newsbeuter')

source=('git+https://github.com/akrennmair/newsbeuter.git')

sha1sums=('SKIP')

pkgver() {
    cd newsbeuter
    _tag=$(git tag --sort=v:refname | sed -n '$p')
    printf '%s.r%s.g%s' "${_tag#r}" "$(git rev-list "$_tag"..HEAD --count)" "$(git rev-parse --short HEAD)"
}

build() {
    cd newsbeuter
    ./config.sh
    make
    make doc
}

package() {
    cd newsbeuter
    make prefix=/usr DESTDIR="$pkgdir" install
    install -Dm0644 LICENSE "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE
}
