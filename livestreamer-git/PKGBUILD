pkgname=livestreamer-git
pkgver=1.9.0.r59.g012e6ae
pkgrel=1

pkgdesc='CLI program that helps launch various streaming services in a real player instead of flash.'
url='https://github.com/chrippa/livestreamer' 
arch=('any')
license=('GPL3')

depends=('python' 'rtmpdump' 'python-requests')
makedepends=('git' 'python-sphinx')

conflicts=('livestreamer')
provides=('livestreamer')

source=('git://github.com/chrippa/livestreamer.git#branch=develop')

md5sums=('SKIP')

pkgver(){
    cd livestreamer
    git describe | sed 's/^v//; s/-/.r/; s/-/./'
}

build() {
    cd livestreamer
    python3 setup.py build
    
    cd docs
    make man
}

package(){
    cd livestreamer
    python3 setup.py install --root="$pkgdir"/ --optimize=1
    install -Dm644 docs/_build/man/livestreamer.1 "$pkgdir"/usr/share/man/man1/livestreamer.1
}
