pkgname=transmission-remote-cli-git
pkgver=1.7.1.r1.g844cc4e
pkgrel=2

pkgdesc='curses frontend to transmission-daemon'
url='https://github.com/fagga/transmission-remote-cli'
arch=('any')
license=('GPL')

depends=('python2')
makedepends=('git')
optdepends=('python2-geoip: guess peer country'
            'adns-python: resolve IP to host names')

provides=('transmission-remote-cli')
conflicts=('transmission-remote-cli')

source=('git+https://github.com/fagga/transmission-remote-cli')

sha1sums=('SKIP')

pkgver() {
    cd transmission-remote-cli
    git describe --long --tags | sed 's/^v//; s/-/.r/; s/-/./'
}

prepare() {
    cd transmission-remote-cli
    sed -i '1 s/python$/&2/' transmission-remote-cli
}

package() {
    cd transmission-remote-cli
    install -Dm755 transmission-remote-cli "$pkgdir"/usr/bin/transmission-remote-cli
    install -Dm644 transmission-remote-cli.1 "$pkgdir"/usr/share/man/man1/transmission-remote-cli.1
    install -Dm644 completion/bash/transmission-remote-cli-bash-completion.sh "$pkgdir"/usr/share/bash-completion/completions/transmission-remote-cli
}
