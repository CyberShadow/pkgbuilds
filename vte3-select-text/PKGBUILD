pkgname=vte3-select-text
pkgver=0.40.0
pkgrel=2

pkgdesc='Virtual Terminal Emulator widget for use with GTK3'
url='http://www.gnome.org'
arch=('i686' 'x86_64')
license=('LGPL')

depends=('gtk3' 'vte-common')
makedepends=('intltool' 'gobject-introspection' 'gtk3' 'python2' 'vala')

source=("http://download.gnome.org/sources/vte/${pkgver:0:4}/vte-$pkgver.tar.xz"
        'expose_select_text.patch')

sha256sums=('54d96181626f2641c78e30ccf991ea124b8713646231e52a973dea3adf02d852'
            '929f8bc647215f1580cc176f816d8793c47d8076a7d5329f4145d2d6aec62b88')

provides=('vte3')
conflicts=('vte3')

options=('!emptydirs')

prepare() {
    cd vte-"$pkgver"
    patch -Np1 -i "$srcdir"/expose_select_text.patch
}

build() {
    cd vte-"$pkgver"
    ./configure --prefix=/usr \
        --enable-gnome-pty-helper \
        --libexecdir=/usr/lib/vte \
        --enable-introspection \
        --localstatedir=/var \
        --sysconfdir=/etc \
        --disable-static
    make
}

package() {
    cd vte-"$pkgver"
    make DESTDIR="$pkgdir" install

    # Provided by vte-common.
    rm "$pkgdir"/etc/profile.d/vte.sh
    rm "$pkgdir"/usr/lib/vte/gnome-pty-helper
}
